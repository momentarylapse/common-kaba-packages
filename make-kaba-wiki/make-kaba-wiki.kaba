use kaba
use os
use time
use common
import doc
import wiki
import docu

func wiki_upload(name: string, wiki: string)
	fs.write_text(name + ".txt", wiki)


func make_documentation()
	wiki_upload("kaba reference", wiki.render_reference())

	for p in weak(packages)
		make_module(p, p.name, "", [])

	var external_packages = find_external()
	for p in external_packages
		make_module(p.main, p.name, "", p.sub_modules)

	var external_libs = find_external_libs()
	for p in external_libs
		make_module(p, p.name, "", [])

func get_func_alternatives(all: Function*[], f: Function) -> Function*[]
	var alt: Function*[]
	for ff in all
		if ff.name == f.name
			alt.add(ff)
	return alt


func make_class(c: Class, ns: string)
	wiki_upload("kaba.{{ns}}.{{c.name}}", wiki.render_class(ns, c))

	var funcs = filter_funcs(c.functions, c)
	for f in funcs
		wiki_upload("kaba.{{ns}}.{{c.name}}.{{f.name}}", wiki.render_func("{{ns}}.{{c.name}}", f, get_func_alternatives(funcs, f)))
	var classes = filter_classes(c.classes)
	for cc in classes
		make_class(cc, "{{ns}}.{{c.name}}")

func make_module(p: Module, name: string, _ns: string, sub_modules: (string,Module*)[])
	string ns = name
	if _ns != ""
		ns = "{{_ns}}.{{name}}"
	print("MAKE MOD {{name}}   {{ns}}")
	wiki_upload("kaba.{{ns}}", wiki.render_module(p, name, _ns, sub_modules))

	var classes = filter_classes(p.classes())
	for c in classes
		make_class(c, ns)

	var funcs = filter_funcs(p.functions(), p.base_class())
	for f in funcs
		wiki_upload("kaba.{{ns}}.{{f.name}}", wiki.render_func(ns, f, get_func_alternatives(p.functions(), f)))

	var vars = p.variables()
	for v in vars
		wiki_upload("kaba.{{ns}}.{{v.name}}", wiki.render_var(ns, v))
	
	for m in sub_modules
		make_module(m.e1, m.e0, ns, [])


func main(arg: string[])
	find_external()
	find_external_libs()
	if len(arg) > 0
		if arg[0] == "prepare"
			docu.report_missing = true
			docu.prepare("doc")
	else
		make_documentation()

