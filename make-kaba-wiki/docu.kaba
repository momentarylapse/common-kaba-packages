use kaba.*
use os
use common.*

var report_missing = false

func parse_doc_string(s: string) -> string
	var ss = s
	while true
		for p in ss.find("<class>", 0)
			for p2 in ss.find("</class>", p)
				let c = ss[p+7:p2]
				let xx = c.explode(".")
				ss = ss[:p] + "'''<tt>[[kaba.{{c}}|{{xx[len(xx)-1]}}]]</tt>'''" + ss[p2+8:]
				continue
		break
	return ss.unescape()


func doc_get(ns: string, name: string, is_class: bool, key: string) -> string
	var p = os.Path("doc/" + ns.replace(".", "/"))
	if is_class
		p = p | name | "class.txt"
	else
		p = p | name + ".txt"
	var conf: os.Configuration
	conf.load(p)
	if str(conf[key]) in ["nil", ""]
		if report_missing
			print("MISSING: {{ns}}.{{name}}:  {{key}}")
		conf[key] = ""
		conf.save(p)
	else if str(conf[key]) == "!"
		return ""
	else
		#print("OK:      {{ns}}.{{name}}:  {{key}} = {{conf[key]}}")
		return parse_doc_string(str(conf[key]))
	return "..."


func doc_get_class_description(ns: string, c: Class) -> string
	return doc_get(ns, c.name, true, "description")

func doc_get_class_element(ns: string, c: Class, e: ClassElement) -> string
	return doc_get(ns, c.name, true, "element:{{e.name}}")

func doc_get_func_description(ns: string, f: Function) -> string
	return doc_get(ns, f.name, false, "description")

func doc_get_func_return(ns: string, f: Function) -> string
	return doc_get(ns, f.name, false, "return")

func doc_get_func_param(ns: string, f: Function, p: string) -> string
	return doc_get(ns, f.name, false, "param:{{p}}")


func short_doc(s: string) -> string
	if len(s) == 0
		return s
	var x = s.explode("\n")[0]
	if len(x) > 100
		x = x[:100] + "..."
	return x


func prepare_func(f: Function, ns: string, dir: os.Path)
	doc_get_func_description(ns, f)
	let params = func_notable_params(f)
	for p in params
		let x = doc_get_func_param(ns, f, p.e0)
	if f.return_type.name != "void"
		let x = doc_get_func_return(ns, f)


func prepare_class(c: Class, ns: string, dir: os.Path)
	if not os.fs.exists(dir)
		os.fs.create_directory(dir)
	print(c)
	
	let funcs = filter_funcs(c.functions, c)
	let elements = filter_elements(c.elements)
	let consts = filter_consts(c.constants)
	let classes = filter_classes(c.classes)
	
	doc_get_class_description(ns, c)
	for e in elements
		let r = doc_get_class_element(ns, c, e)
	
	for f in funcs
		prepare_func(f, ns + "." + c.name, dir)
	
	for cc in classes
		prepare_class(cc, ns + "." + c.name, dir | cc.name)


func prepare_module(p: Module, name: string, dir: os.Path)
	if not os.fs.exists(dir)
		os.fs.create_directory(dir)
	print("PREP MOD   {{name}}  {{dir}}")

	let classes = filter_classes(p.classes())
	for c in classes
		prepare_class(c, name, dir | c.name)

	let funcs = filter_funcs(p.functions(), p.base_class())
	for f in funcs
		prepare_func(f, name, dir)

#	var vars = p.variables()
#	for v in vars
#		wiki_upload("kaba.{{p.name}}.{{v.name}}", show_var(p.name, v))


func prepare(dir: os.Path)
	print("PREPARE")
	for p in default_context.packages
		prepare_module(p.main_module, p.name, dir | p.name)
	print("----")

	let external_packages = find_external_packages()
	for p in external_packages
		for m in p.main
			prepare_module(m, p.name, dir | p.name)
		for m in p.sub_modules
			for mm in m[1]
				prepare_module(mm, p.name + "." + m[0], dir | p.name | m[0])

