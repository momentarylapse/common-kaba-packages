
struct Iter
	var x: i64[32]


func extern _bson_new_from_json(json: u8*, len: i32, err: void*) -> void*
func extern _bson_as_canonical_extended_json(bson: void*, __len: void*) -> u8[0]&
func extern _bson_as_relaxed_extended_json(bson: void*, __len: void*) -> u8[0]&
func extern _bson_free(str: void*)
func extern _bson_destroy(bson: void*)
func extern _bson_new_from_data(data: u8*, length: i32) -> void*
func extern _bson_iter_init(i: Iter&, bson: void*) -> bool
func extern _bson_iter_next(i: Iter&) -> bool
func extern _bson_iter_key(i: Iter&) -> u8[0]&
func extern _bson_iter_int32(i: Iter&) -> i32
func extern _bson_iter_int64(i: Iter&) -> i64
func extern _bson_iter_double(i: Iter&) -> f64
func extern _bson_iter_utf8(i: Iter&, __len: void*) -> u8[0]&
func extern _bson_iter_array(i: Iter&, len: i32&, a: void*)
func extern _bson_iter_oid(i: Iter&) -> void*
func extern _bson_oid_to_string(oid: void*, str: u8*)
func extern _bson_iter_type(i: Iter&) -> i32
func extern _bson_iter_recurse(parent: Iter&, child: Iter&) -> bool


func char_to_str(p: u8[0]&) -> string
	var t: string
	for i in 0:10000
		if p[i] == 0
			break
		t.add(p[i])
	return t


func json_to_bson(json: string) -> void*
	var t = json
	t.add(0)
	var b = _bson_new_from_json(&t[0], len(json), nil)
	if !b
		raise(new Exception("invalid json: {{json}}"))
	return b


func bson_to_json(bson: void*) -> string
	#char[0]* x = bson_as_canonical_extended_json(bson, nil)
	var x: u8[0]& = _bson_as_relaxed_extended_json(bson, nil)
	var t = char_to_str(x)
	_bson_free(x)
	return t


func bson_iter_key(i: Iter&) -> string
	return char_to_str(_bson_iter_key(i))
func bson_iter_str(i: Iter&) -> string
	return char_to_str(_bson_iter_utf8(i, nil))


func bson_iter_str_array(i: Iter&) -> string[]
	var r: string[]
	var itarray: Iter
	var length = 0
	var array: u8*
	_bson_iter_array(i, &length, &array)
	var bson = _bson_new_from_data(array, length)
	_bson_iter_init(&itarray, bson)
	while _bson_iter_next(&itarray)
		r.add(bson_iter_str(&itarray))
	return r



func bson_iter_to_any(iter: Iter&) -> Any
	let t = _bson_iter_type(iter)
	#print("iter to any {{t}}")
	if t == 2 # str utf8
		return bson_iter_str(iter)
	else if t == 16 # i32
		return _bson_iter_int32(iter)
	else if t == 18 # i64
		return i32(_bson_iter_int64(iter))
	else if t == 1 # f64
		return f32(_bson_iter_double(iter))
	else if t == 4 # array
		var r: Any
		var itarray: Iter
		_bson_iter_recurse(&iter, &itarray)
		while _bson_iter_next(&itarray)
			r.add(bson_iter_to_any(&itarray))
		return r
	else if t == 7 # ID
		var oid = _bson_iter_oid(iter)
		var s: string
		s.resize(25)
		_bson_oid_to_string(oid, &s[0])
		s.resize(24)
		return s
	else if t == 10 # null
		var r: Any
		return r
	else if t == 3 or t >= 250 # map
		var it2: Iter
		_bson_iter_recurse(&iter, &it2)
		return bson_iter_map_to_any(&it2)
	raise(new Exception("UNKNOWN TYPE: {{t}}"))


func bson_iter_map_to_any(iter: Iter&) -> Any
	var r: Any
	while _bson_iter_next(iter)
		let key = bson_iter_key(iter)
		r[key] = bson_iter_to_any(iter)
	return r

func bson_to_any(doc: void*) -> Any
	#print("BBB " + bson_to_json(doc))
	var iter: Iter
	_bson_iter_init(&iter, doc)
	return bson_iter_map_to_any(&iter)


