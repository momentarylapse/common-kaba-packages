use "config.kaba"
use "localdb.kaba"
use "tag.kaba"


string scp_escape(string s)
	string r = s
	r = r.replace("(", "\\\\\\(")
	r = r.replace(")", "\\\\\\)")
	r = r.replace("'", "\\\\\\'")
	r = r.replace(" ", "\\\\\\ ")
	return r

string ssh_escape(string s)
	string r = s
	r = r.replace(" ", "\\\\ ")
	r = r.replace("(", "\\\\(")
	r = r.replace(")", "\\\\)")
	r = r.replace("'", "\\\\'")
	return r


class RemoteFile extends LocalFile

RemoteFile[] remote_parse(string r, string options)
	RemoteFile[] rfiles
	string[] list = r.explode("\n")
	for l in list
		string[] parts = l.explode("\t")
		if parts.num < 7
			continue
		RemoteFile rf
		rf.id = parts[0]
		rf.mtime = parts[1].int()
		rf.size = parts[2].int()
		rf.hash = parts[3]
		rf.remote_path = parts[4]
		rf.name = parts[5]
		string extension = parts[6]
		rf.path = rf.name + "." + extension // temporary
		for i in 8:parts.num
			rf.tags.add(str2tag(parts[i]))
		rf.options = options.explode(",")
		if parts[7] == "m"
			rf.set("quality=medium")
		rf.set(NONLOCAL)
		rfiles.add(rf)
	return rfiles

RemoteFile[] remote_find(string filter, string options)
	string cmd = "ssh " + SERVER + " command=\""
	
	string quality = "perfect"
	if options.find("quality=medium", 0) >= 0
		quality = "medium"
	cmd += "php " + YII + " file/find-sisyphos \\\"" + ssh_escape(filter) + "\\\" " + quality + "; "
	cmd += "\""
	if conf.verbose
		print(cmd)
	_exec_(cmd + " > " + TEMP_OUT)
		
	//tocopy.clear()
	//copysize = 0
	
	string r = FileRead(TEMP_OUT).trim()
	return remote_parse(r, options)
	//string[] rs = r.explode("-=-=-=-=-=\n")
	//return rs

RemoteFile[] remote_info(string id, string options)
	string cmd = "ssh " + SERVER + " command=\""
	
	string quality = "perfect"
	if options.find("quality=medium", 0) >= 0
		quality = "medium"
	cmd += "php " + YII + " file/info-sisyphos " + id + " " + quality + "; "
	cmd += "\""
	if conf.verbose
		print(cmd)
	_exec_(cmd + " > " + TEMP_OUT)
		
	//tocopy.clear()
	//copysize = 0
	
	string r = FileRead(TEMP_OUT).trim()
	return remote_parse(r, options)
	//string[] rs = r.explode("-=-=-=-=-=\n")
	//return rs

RemoteFile[] remote_find_by_hash(string hashes)
	string cmd = "ssh " + SERVER + " command=\""
	
	cmd += "php " + YII + " file/info-by-hash-sisyphos " + hashes + "; "
	cmd += "\""
	if conf.verbose
		print(cmd)
	_exec_(cmd + " > " + TEMP_OUT)

	string r = FileRead(TEMP_OUT).trim()
	return remote_parse(r, "")

RemoteFile[] remote_find_by_id(string ids, string options)
	string cmd = "ssh " + SERVER + " command=\""
	
	string quality = "perfect"
	if options.find("quality=medium", 0) >= 0
		quality = "medium"
	cmd += "php " + YII + " file/info-by-id-sisyphos " + ids + " " + quality + "; "
	cmd += "\""
	if conf.verbose
		print(cmd)
	_exec_(cmd + " > " + TEMP_OUT)
	
	string r = FileRead(TEMP_OUT).trim()
	return remote_parse(r, "")



void sync_add_pure_remote(RemoteFile rf, ConfigDir e, ConfigDirSync sync)
	LocalFile *lf = db.find_by_id(rf.id)
	if !lf
		//string rel_name = sync.guess_dir(["parts"]) + rf.rel_name.replace("/", " - ")
		lf = db.find_by_hash(rf.hash)
		if lf
			if lf.id.num > 0
				print("HASH COLLISION  " + rel_path(lf.path) + "  <->  " + "REMOTE...")
			lf.id = rf.id
			//if conf.verbose
			print("RELINK  " + rel_path(lf.path))// + "  ->  " + rf.id)
			lf.sync_tags(rf.tags)
			lf.name = rf.name
			return
	
		// new remote file
		LocalFile ll = rf
		ll.path = ll.apply_structure(e)
		ll.set(NONLOCAL)
		db.files.add(ll)
		print("ADD PURE REMOTE  " + rel_path(ll.path))
		
		if conf.show_meta or conf.verbose
			ll.show_info("      ")


void remote_sync_by_filter(string dir)
	ConfigDir* e = conf.get_cfg(dir)
	if !e
		return
	for s in e.syncs
		RemoteFile[] rfiles = remote_find(s.filter, s.options)
		for rf in rfiles
			LocalFile *lf = db.find_by_id(rf.id)
			if lf
				sync_file(rf, lf)
			else
				sync_add_pure_remote(rf, *e, s)
		db.save()


void remote_download(LocalFile* f)
	if !f.in_db()
		print("ERROR: not in db")
		return

	if !f.pure_remote() and !f.is(CHANGED_FILE)
		print("ERROR: neither pure remote nor changed")
		return
	if f.remote_path.num == 0
		print("ERROR: no remote path")
		return
	
	make_all_dirs(f.path)
	string cmd = "scp"
	string rp = scp_escape(f.remote_path)
	cmd += " " + SERVER + ":" + rp
	cmd += " \"" + bash_escape(f.path) + "\""
	if conf.verbose
		print(cmd)
	_exec_(cmd)

	File *ff = FileOpen(f.path)
	if ff
		f.size = ff.getSize()
		f.mtime = ff.getMDate().time
		f.hash = FileHash(f.path, "md5")
		f.unset(NONLOCAL)
		f.unset(CHANGED_FILE)
		db.save()
		delete ff
		return
	print("---ERROR---")


void remote_upload(LocalFile* f)
	
	if !f.pure_local()
		print("ERROR: not pure local")
		return
	
	if f.path.extension() == ""
		print("ERROR: remote does not like files without extension: " + rel_path(f.path))
		return
	
	f.remote_path = ALEX_IN + f.hash + "." + f.path.extension()
	
	string cmd = "scp"
	string rp = scp_escape(f.remote_path)
	cmd += " \"" + bash_escape(f.path) + "\""
	cmd += " " + SERVER + ":" + rp
	if conf.verbose
		print(cmd)
	_exec_(cmd)
	

	string tags = tags2str(f.tags)	
	
	cmd = "ssh " + SERVER + " command=\""
	cmd += "php " + YII + " file/upload-sisyphos \\\"" + ssh_escape(f.remote_path) + "\\\" \\\"" + f.name + "\\\" " + f.hash + " \\\"" + tags + "\\\" ; "
	cmd += "\""
	if conf.verbose
		print(cmd)
	_exec_(cmd + " > " + TEMP_OUT)
	
	string r = FileRead(TEMP_OUT).trim()
	
	string[] p = r.explode("\t")
	if p.num >= 2
		if p[0] == "ok"
			f.id = p[1]
			db.save()
			return
		if p[0] == "duplicate"
			print("hash already in db  -> RELINK")
			f.id = p[1]
			db.save()
			return
		if p[0] == "bad hash"
			print("ERROR: upload failed?")
			return
	print("ERROR: ????")


void remote_upload_meta(LocalFile* f)
	
	if f.pure_local()
		print("ERROR: pure local")
		return

	string tags = tags2str(f.tags)	
	
	string cmd = "ssh " + SERVER + " command=\""
	cmd += "php " + YII + " file/override-meta-sisyphos " + f.id + " \\\"" + f.name + "\\\" \\\"" + tags + "\\\" ; "
	cmd += "\""
	if conf.verbose
		print(cmd)
	_exec_(cmd + " > " + TEMP_OUT)
	
	string r = FileRead(TEMP_OUT).trim()
	
	if r == "ok"
		if conf.verbose
			print("yay")
		f.unset(CHANGED_META)
		db.save()
		return
	if r == "not found"
		print("ERROR: remote did not find")
		return
	print("ERROR: ????")



void remote_download_meta(LocalFile* f)
	if f.id.num == 0
		print("ERROR: no id")
		return
	RemoteFile[] rfiles = remote_info(f.id, f.options)
	for rf in rfiles
		print("META  " + rel_path(f.path))
		f.unset(CHANGED_META)
		f.name = rf.name
		f.tags = rf.tags
		db.dirty = true

void remote_sync_by_id(string _dir)
	LocalFile*[] files = db.get_in_dir(_dir)
	string[] ids_perfect
	string[] ids_medium
	for f in files
		if !f.pure_remote() and !f.pure_local()
			if f.is("quality=medium")
				ids_medium.add(f.id)
			else
				ids_perfect.add(f.id)
	RemoteFile[] rfiles, rfiles_medium
	if ids_perfect.num > 0
		rfiles = remote_find_by_id(ids_perfect.join(","), "")
	if ids_medium.num > 0
		rfiles_medium = remote_find_by_id(ids_medium.join(","), "quality=medium")
	for rf in rfiles_medium
		rfiles.add(rf)
	for rf in rfiles
		LocalFile* lf = db.find_by_id(rf.id)
		if lf
			sync_file(rf, lf)

void sync_file(RemoteFile rf, LocalFile* lf)
	lf.unset(CHANGED_FILE)
	lf.unset(CHANGED_META)
	lf.remote_path = rf.remote_path
	db.dirty = true
	if rf.is("quality=medium")
		lf.set("quality=medium")
	if lf.pure_local()
		print("RELINK  " + rel_path(lf.path))
		lf.id = rf.id
		//db.dirty = true
	if lf.hash != rf.hash
		print("CHANGED HASH  " + rel_path(lf.path))
		lf.set(CHANGED_FILE)
		//db.dirty = true
	if lf.name != rf.name
		print("CHANGED NAME  " + rel_path(lf.path))
		if conf.verbose
			print(lf.name + " <-> " + rf.name)
		lf.set(CHANGED_META)
		//db.dirty = true
	if !tags_equal(lf.tags, rf.tags)
		print("CHANGED TAGS  " + rel_path(lf.path))
		if conf.verbose
			print(tags2str(tags_sort(lf.tags)))
			print(tags2str(tags_sort(rf.tags)))
		lf.set(CHANGED_META)
		//db.dirty = true


void remote_sync_pure_locals_by_hash(string _dir)
	LocalFile*[] files = db.get_in_dir(_dir)
	string[] hashes
	for f in files
		if f.pure_local()
			hashes.add(f.hash)
	if hashes.num == 0
		return
	RemoteFile[] rfiles = remote_find_by_hash(hashes.join(","))
	for rf in rfiles
		if conf.verbose
			print(rf.hash)
		LocalFile* lf = db.find_by_hash(rf.hash)
		if lf
			if conf.verbose
				print(lf.path)
			sync_file(rf, lf)

