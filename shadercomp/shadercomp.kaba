use os

enum Type
	VERTEX
	FRAGMENT
	GEOMETRY
	COMPUTE
	RAYGEN
	RAYMISS
	CLOSESTHIT
	ANYHIT

func tag_contents(all: string, tag: string) -> string
	var i0 = all.find("<{{tag}}>", 0)
	if i0 < 0
		return ""
	var i1 = all.find("</{{tag}}>", i0)
	if i1 < 0
		return ""
	return all[i0+len(tag)+2:i1]

var cur_dir: Path

func compile_stage(code: string, layout: LayoutData, stage: string) -> string
	if code == ""
		return ""
	var itmp = cur_dir << "shadercomp.glsl"
	var otmp = cur_dir << "shadercomp.bin"
	string c = code
	if len(layout.common) > 0
		c = layout.common + "\n" + c
	for e in layout.extensions
		c = "#extension {{e}} : require\n" + c
	if len(layout.version) > 0
		c = "#version {{layout.version}}\n" + c
	try
		os.fs.write_text(itmp, c)
#		shell_execute("echo '{{code}}' > {{itmp}}")
#		print("glslangValidator -V -S {{stage}} {{itmp}} -o {{otmp}}")
		shell_execute("glslangValidator -V -S {{stage}} {{itmp}} -o {{otmp}} &> /tmp/shader-error")
	except Exception as e
		string x = shell_execute("cat /tmp/shader-error")
		print("  ----  ERROR ({{stage}})  ----\n{{x}}")
		return ""
	string x = shell_execute("cat {{otmp}}")
	os.fs.delete(itmp)
	os.fs.delete(otmp)
	
#	string cmd = "echo '{{code}}' | glslc -fshader-stage={{stage}} - -o -"
	# print(cmd)
#	string x = shell_execute(cmd)
	# print(len(x))
	return x

class LayoutData
	var bindings: string
	var input: string
	var topology: string
	var push_size: int
	var version: string
	var extensions: string[]
	var common: string

func parse_layout(layout: string) -> LayoutData
	LayoutData ld
	ld.push_size = 0
	ld.topology = "triangles"
	var ll = layout.explode("\n")
	for s in ll
		var xx = s.explode("=")
		if len(xx) < 2
			continue
		var key = xx[0].trim()
		var value = xx[1].trim()
		if key == "bindings"
			ld.bindings = value
		else if key == "pushsize"
			ld.push_size = int(value)
		else if key == "input"
			ld.input = value
		else if key == "topology"
			ld.topology = value
		else if key == "version"
			ld.version = value
		else if key == "extensions"
			ld.extensions = value.explode(",")
		else
			print("WARNING: layout info unknown: {{s}}")
	return ld
func extract_push_size(layout: string) -> int
	return 0

func compile(filename: Path)
	cur_dir = filename.parent()
	var all = fs.read_text(filename)
	var layout = tag_contents(all, "Layout")
	var common = tag_contents(all, "Common")
	var code_vertex = tag_contents(all, "VertexShader")
	var code_geometry = tag_contents(all, "GeometryShader")
	var code_fragment = tag_contents(all, "FragmentShader")
	var code_ray_gen = tag_contents(all, "RayGenShader")
	var code_ray_miss = tag_contents(all, "RayMissShader")
	var code_ray_closest_hit = tag_contents(all, "RayClosestHitShader")
	
	var ld = parse_layout(layout)
	ld.common = common
	
	var x_vertex = compile_stage(code_vertex, ld, "vert")
	var x_geometry = compile_stage(code_geometry, ld, "geom")
	var x_fragment = compile_stage(code_fragment, ld, "frag")
	var x_ray_gen = compile_stage(code_ray_gen, ld, "rgen")
	var x_ray_miss = compile_stage(code_ray_miss, ld, "rmiss")
	var x_ray_closest_hit = compile_stage(code_ray_closest_hit, ld, "rchit")
	
	var f = new BinaryFormatter(fs.open(Path(str(filename) + ".compiled"), "wb"))
	f << "Info"
	f << "{version=0.1,shaders=SPIR-V}"
	f << "Input"
	f << ld.input
	f << "Topology"
	f << ld.topology
	f << "Bindings"
	f << ld.bindings
	f << "PushSize"
	f << str(ld.push_size)
	if len(x_vertex) > 0
		f << "VertexShader"
		f << x_vertex
	if len(x_geometry) > 0
		f << "GeometryShader"
		f << x_geometry
	if len(x_fragment) > 0
		f << "FragmentShader"
		f << x_fragment
	if len(x_ray_gen) > 0
		f << "RayGenShader"
		f << x_ray_gen
	if len(x_ray_miss) > 0
		f << "RayMissShader"
		f << x_ray_miss
	if len(x_ray_closest_hit) > 0
		f << "RayClosestHitShader"
		f << x_ray_closest_hit
	del f

func main(arg: string[])
	for a in arg
		compile(a)
	if len(arg) == 0
		print("input files?")
