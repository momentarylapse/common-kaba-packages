

class Tag
	string key
	string value
	void __init__(string k, string v)
		key = k
		value = v
	string str()
		return key + "=" + value
	string strh()
		return key + "=" + value.hex(false)
	bool __lt__(Tag o)
		if key == o.key
			return value < o.value
		return key < o.key
	bool match(Tag filter)
		return key == filter.key and value.match(filter.value)

Tag[] tags_sort(Tag[] tags)
	Tag[] r = tags
	for i in 0:r.num
		for j in i+1:r.num
			if r[j] < r[i]
				Tag t = r[i]
				r[i] = r[j]
				r[j] = t
	return r

bool tags_equal(Tag[] a, Tag[] b)
	return tags2str(tags_sort(a)) == tags2str(tags_sort(b))

string tags2str(Tag[] tags)
	string r
	for t,i in tags
		if i > 0
			r += ","
		r += t.str()
	return r

string tags2strh(Tag[] tags)
	string r
	for t,i in tags
		if i > 0
			r += ","
		r += t.strh()
	return r

Tag str2tag(string s)
	string[] a = s.explode("=")
	Tag t
	t.key = a[0]
	t.value = a[1]
	return t

Tag strh2tag(string s)
	string[] a = s.explode("=")
	Tag t
	t.key = a[0]
	t.value = a[1].unhex()
	return t


Tag[] strh2tags(string s)
	Tag[] tags
	string[] a = s.explode(",")
	for b in a
		tags.add(strh2tag(b))
	return tags


Tag[] str2tags(string s)
	Tag[] tags
	string[] a = s.explode(",")
	for b in a
		tags.add(str2tag(b))
	return tags

bool tag_multi(string key)
	if key == "artist"
		return true
	if key == "author"
		return true
	if key == "category"
		return true
	if key == "genre"
		return true
	return false
