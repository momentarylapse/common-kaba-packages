use os
use config
use localdb
use config
use remote
use dblister



func _action_pull(path: Path)
	set_working_dir(path)
	
	var files = lister.find(path)
	var need_meta: LocalFile[]
	for f in files
		if f.pure_remote() or f.is(CHANGED_FILE)
			remote_download(f)
		if f.is(CHANGED_META)
			need_meta.add(f)

	if len(need_meta) > 0
		if conf.verbosity >= 2
			print("downloading meta data")
		remote_download_meta(need_meta)

func action_pull(arg: string[])
	if len(arg) == 0
		_action_pull(make_path("./"))
	for a in arg
		_action_pull(make_path(a))



func _action_push(path: Path)
	set_working_dir(path)
	
	var files = lister.find(path)
	for f in files
		if not f.in_db()
			continue
		if f.pure_local() or f.is(CHANGED_FILE)
			remote_upload(f)
		#else if f.is(CHANGED_FILE)
		#	print("ERROR  can't push edited files yet")
		if f.is(CHANGED_META)
			log_ok("uploading meta", rel_path(f.path))
			remote_upload_meta(f)

func action_push(arg: string[])
	if len(arg) == 0
		_action_push(make_path("./"))
	for a in arg
		_action_push(make_path(a))



func make_path_safe_for_download(out f: LocalFile) -> bool
	if db.has_path(f.path) or fs.exists(f.path)
		var ff = db.find_by_path(f.path)
		if ff.id == f.id
			# same path and id -> no need to download
			return false
	else
		return true
	
	# try alternative
	var ext = f.path.extension()
	f.path = Path(str(f.path)[:-len(ext)] + "{{f.id}}.{{ext}}")
	if db.has_path(f.path) or fs.exists(f.path)
		return false
	return true

func _get_file(out f: LocalFile, dest: string)
	f.path = Path(f.apply_structure(dest))
	if not make_path_safe_for_download(f)
		log_warn("exists", rel_path(f.path))
		return
		
	log_ok("add", rel_path(f.path))
	
	if conf.dry_run
		return
	db.files.add(f)
	make_all_dirs(f.path)

func _action_get(filter: string, dest: string)
	var files = remote_find(filter, conf.default_quality)
	for f in files
		_get_file(f, dest)


func action_get(arg: string[])
	if len(arg) >= 2
		_action_get(arg[0], make_path(arg[1]))
	else
		_action_get(arg[0], make_path(conf.default_structure))
