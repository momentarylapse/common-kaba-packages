use os
use hui
use config
use localdb
use file
use tag


const CHUNK_SIZE = 128

func scp_escape(s: string) -> string
	var r = s
	r = r.replace("(", "\\\\\\(")
	r = r.replace(")", "\\\\\\)")
	r = r.replace("'", "\\\\\\'")
	r = r.replace(" ", "\\\\\\ ")
	return r

func ssh_escape(s: string) -> string
	var r = s
	r = r.replace(" ", "\\\\ ")
	r = r.replace("(", "\\\\(")
	r = r.replace(")", "\\\\)")
	r = r.replace("'", "\\\\'")
	return r

func scp_download(source: string, dest: string)
	var cmd = "scp -Tp"
	cmd += " {{conf.server}}:\"{{bash_escape2(source)}}\" \"{{bash_escape(dest)}}\""
	if conf.verbosity >= 2
		print(cmd)
	shell_execute(cmd)

func scp_upload(source: string, dest: string)
	var cmd = "scp -Tp \"{{bash_escape(source)}}\" {{conf.server}}:{{scp_escape(dest)}}"
	if conf.verbosity >= 2
		print(cmd)
	shell_execute(cmd)

func remote_interface(action: string, params: string[]) -> string
	var cmd = "ssh {{conf.server}} python3 /home/michi/Tools/alex/if-{{action}}.py"
	for p in params
		cmd += " \\\"{{ssh_escape(p)}}\\\""
	#cmd += ""
	if conf.verbosity >= 2
		print(cmd)
	return shell_exec(cmd).trim()

func call_alex(action: string, params: string[]) -> string
	var cmd = "ssh {{conf.server}} {{CMD}} {{action}}"
	for p in params
		#cmd += " \\\"" + ssh_escape(p) + "\\\""
		cmd += " " + ssh_escape(p)
	#cmd += ""
	if conf.verbosity >= 2
		print(cmd)
	return shell_exec(cmd).trim()

class RemoteFile extends LocalFile

func remote_parse(r: string) -> RemoteFile[]
	if conf.verbosity > 3
		print(r)
	var rfiles: RemoteFile[]
	let list = r.explode("\n")
	for l in list
		let parts = l.explode("\t")
		if len(parts) < 7
			continue
		var rf: RemoteFile
		rf.id = parts[0]
		rf.mtime = int(parts[1])
		rf.size = int(parts[2])
		rf.hash = parts[3]
		#rf.remote_path = parts[4]
		rf.name = parts[5]
		var extension = parts[6]
		rf.path = Path("{{rf.name}}.{{extension}}") # temporary
		rf.path = absolute_path(Path(rf.apply_structure(conf.default_structure)))
		for p in parts[8:]
			rf.tags.add(alex2tag(p))
		if parts[7] == "m"
			rf.set("quality=medium")
		rf.set(NONLOCAL)
		rfiles.add(rf)
	return rfiles

func remote_find(filter: string, quality: string) -> RemoteFile[]
	let r = call_alex("find", [filter, "--{{quality}}"])
	return remote_parse(r)

func remote_find_by_hash_chunk(hashes: string[]) -> RemoteFile[]
	let r = call_alex("sp by-hash", hashes)
	return remote_parse(r)

func remote_find_by_id_chunk(ids: string[], quality: string) -> RemoteFile[]
	var cmd = "sp by-id"
	if quality == MEDIUM
		cmd = "sp by-id-medium"
	let r = call_alex(cmd, ids)
	return remote_parse(r)

func remote_find_by_hash(hashes: string[]) -> RemoteFile[]
	var rr: RemoteFile[]
	var done = 0
	while done < len(hashes)
		let ee = min(done + CHUNK_SIZE, len(hashes))
		if conf.verbosity >= 2
			print("hash... {{done}}:{{ee}} of {{len(hashes)}}")
		let r = remote_find_by_hash_chunk(hashes[done:ee])
		for x in r
			rr.add(x)
		done = ee
	return rr

func remote_find_by_id(ids: string[], quality: string) -> RemoteFile[]
	var rr: RemoteFile[]
	var done = 0
	while done < len(ids)
		let ee = min(done + CHUNK_SIZE, len(ids))
		if conf.verbosity >= 2
			print("ids... {{done}}:{{ee}} of {{len(ids)}}")
		let r = remote_find_by_id_chunk(ids[done:ee], quality)
		for x in r
			rr.add(x)
		done = ee
	return rr



func sync_add_pure_remote(rf: RemoteFile, root: Path, structure: string)
	if not db.has_id(rf.id)
		#string rel_name = sync.guess_dir(["parts"]) + rf.rel_name.replace("/", " - ")
		try
			var lf = db.by_hash(rf.hash)
			if len(lf.id) > 0
				log_error("hash collision", "{{rel_path(lf.path)}}  <->  REMOTE...")
			lf.id = rf.id
			#if conf.verbose
			log_warn("relink", "{{rel_path(lf.path)}}") # + "  ->  " + rf.id)
			lf.sync_tags(rf.tags)
			lf.name = rf.name
			return
		except
			# new remote file
			var ll = rf
			ll.path = root << ll.apply_structure(structure)
			ll.set(NONLOCAL)
			db.save([ll])
			log_ok("add pure remote", str(rel_path(ll.path)))
		
			if conf.show_meta or conf.verbosity >= 2
				ll.show_info("      ")


func remote_sync_by_filter(filter: string, quality: string, structure: string, dir: Path)
	let rfiles = remote_find(filter, quality)
	for rf in rfiles
		try
			var lf = db.find_by_id(rf.id)
			sync_file(rf, lf)
		except
			sync_add_pure_remote(rf, dir, structure)
	print("TODO save")

func remote_download(out f: LocalFile)
	log_ok("downloading", str(rel_path(f.path)))
	if not f.in_db()
		log_error("error", "not in db")
		return

	if not f.pure_remote() and not f.is(CHANGED_FILE)
		log_error("error", "neither pure remote nor changed")
		return
	
	make_all_dirs(f.path)
	scp_download(f.remote_path(), str(f.path))

	f.size = fs.size(f.path)
	f.mtime = fs.mtime(f.path).time
	f.hash = fs.hash(f.path, "md5")
	f.unset(NONLOCAL)
	f.unset(CHANGED_FILE)
	db.save([f])


func remote_upload(out f: LocalFile)
	log_ok("uploading", str(rel_path(f.path)))
	
	if not f.pure_local() and not f.is(CHANGED_FILE)
		log_error("error", "no need to push")
		return
	
	if f.path.extension() == ""
		log_error("error", "remote does not like files without extension: {{rel_path(f.path)}}")
		return
	
	var _remote_path = ALEX_IN + "{{f.hash}}.{{f.path.extension()}}"
	
	scp_upload(f.path, Path(_remote_path))
	
	if not f.pure_local()
		log_error("todo", "upload edited file")
		return
	
	let r = call_alex("sp upload", [_remote_path])
	if r[:2] != "ok"
		log_error("error", "upload... {{r}}")
		return
	let nnn = r[3:].find("\n", 0)
	if nnn >= 0
		log_warn("remote warning", "{{r[4+nnn:]}}")
	f.id = r[3:].explode("\n")[0]
	if conf.verbosity >= 2
		print("ID: {{f.id}}")
	db.save([f])

	#string tagsh = tags2strh(f.tags)
		
	#string r = yii("upload", [f.remote_path, f.name.hex(), f.hash, f.mtime.str(), tagsh])
	#print("answer: " + r)
	
	#string[] p = r.explode("\t")
#	if len(p) >= 2
#		if p[0] == "ok"
#			f.id = p[1]
#			db.save()
#			return
#		if p[0] == "duplicate"
#			print("hash already in db  -> RELINK")
#			f.id = p[1]
#			db.save()
#			return
#		if p[0] == "bad hash"
#			print("ERROR: upload failed?")
#			return
#	print("ERROR: ????")


func remote_upload_meta(out f: LocalFile)
	
	if f.pure_local()
		log_error("error", "pure local")
		return

	let tags = tags2strh(f.tags)	
	
	#string r = remote_interface("override-meta", [f.id, f.name.hex(), tags])
	let r = call_alex("sp override-meta", [f.id, f.name.hex(), tags])
	
	if r.tail(2) == "ok"
		if conf.verbosity >= 2
			print("yay")
		f.unset(CHANGED_META)
		db.save([f])
		return
	if r == "not found"
		log_error("error", "remote did not find")
		return
	print(r)
	log_error("error", "????")


func remote_get_meta(files: LocalFile[]) -> RemoteFile[]
	var ids_perfect: string[]
	var ids_medium: string[]
	for f in files
		if f.is("quality=medium")
			ids_medium.add(f.id)
		else
			ids_perfect.add(f.id)
	var rfiles, rfiles_medium: RemoteFile[]
	if len(ids_perfect) > 0
		rfiles = remote_find_by_id(ids_perfect, PERFECT)
	if len(ids_medium) > 0
		rfiles_medium = remote_find_by_id(ids_medium, MEDIUM)
	for rf in rfiles_medium
		rfiles.add(rf)
	return rfiles

func remote_download_meta(out files: LocalFile[])
	var rfiles = remote_get_meta(files)
	for rf in rfiles
		for lf in files
			if rf.id == lf.id
				log_ok("meta", rel_path(lf.path))
				if conf.pull_tags_only_add
					for t in rf.tags
						if tag_multi(t.key)
							lf.tag_add(t)
						else
							lf.tag_set(t)
				else
					lf.name = rf.name
					lf.tags = rf.tags
					lf.unset(CHANGED_META)
				db.save([lf])

func remote_sync_by_id(out files: LocalFile[])
	var files_wanted: LocalFile[]
	for f in files
		if not f.pure_remote() and not f.pure_local()
			files_wanted.add(f)
	let rfiles = remote_get_meta(files_wanted)
	for rf in rfiles
		for lf in files
			if lf.id == rf.id
				sync_file(rf, lf)

func sync_file(rf: RemoteFile, out lf: LocalFile)
	if conf.verbosity >= 2
		print("sync {{lf.path}}")
	lf.unset(CHANGED_FILE)
	lf.unset(CHANGED_META)
	if rf.is("quality=medium")
		lf.set("quality=medium")
	if lf.pure_local()
		if conf.verbosity >= 1
			log_warn("relink", rel_path(lf.path))
		lf.id = rf.id
	if lf.hash != rf.hash
		if conf.verbosity >= 1
			log_warn("changed hash", rel_path(lf.path))
		if conf.verbosity >= 2
			print("{{lf.hash}} <-> {{rf.hash}}")
		lf.set(CHANGED_FILE)
	if lf.name != rf.name
		if conf.verbosity >= 1
			log_warn("changed name", rel_path(lf.path))
		if conf.verbosity >= 2
			print("{{lf.name}} <-> {{rf.name}}")
		lf.set(CHANGED_META)
	if not tags_equal(lf.tags, rf.tags)
		if conf.verbosity >= 1
			log_warn("changed tags", rel_path(lf.path))
		if conf.verbosity >= 2
			print(tags2str(tags_sort(lf.tags)))
			print(tags2str(tags_sort(rf.tags)))
		lf.set(CHANGED_META)
	db.save([lf])


func remote_sync_pure_locals_by_hash(out files: LocalFile[])
	var hashes: string[]
	for f in files
		if f.pure_local()
			hashes.add(f.hash)
	if len(hashes) == 0
		return
	var rfiles = remote_find_by_hash(hashes)
	for rf in rfiles
		if conf.verbosity >= 2
			print(rf.hash)
		for lf in files
			if lf.hash == rf.hash
				sync_file(rf, lf)

