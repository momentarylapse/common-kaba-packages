use localdb
use config
use os

# mix db files with "real" files
class DBLister
	var paths: Path[]
	var fake_files: LocalFile[]
	
	func is_ignored(path: Path) -> bool
		for i in conf.ignore
			if str(path).match(i)
				return true
		return false

	func find_paths(dir: Path, recursive: bool)
		var list = Filesystem.search(dir, "*", "fd")
		for e in list
			if Filesystem.is_directory(dir << e)
				if str(e).head(1) != "." and recursive
					find_paths(dir << e, recursive)
			else
				var path = dir << e
				if !is_ignored(path)
					paths.add(path)
	
	func find(dir: Path) -> LocalFile*[]
		if Filesystem.is_directory(dir)
			return find_dir(dir)
		return find_file(dir)
		
		
	func find_file(path: Path) -> LocalFile*[]
		var files: LocalFile*[]
		fake_files.clear()
		paths.clear()
		
		try
			# in DB?
			return [db.find_by_path(path)]
		except
			if Filesystem.exists(path)
				LocalFile ff
				ff.path = path
				fake_files.add(ff)
				return [&fake_files[0]]
			else
				log_error("error", "can't find '{{path}}'")
		return files
		
	func find_dir(dir: Path) -> LocalFile*[]
		var files: LocalFile*[]
		fake_files.clear()
		paths.clear()

		files = db.get_in_dir(dir, conf.recursive)
		
		find_paths(dir, conf.recursive)
		for p in paths
			try
				db.find_by_path(p)
			except
				LocalFile f
				f.path = p
				fake_files.add(f)
		for f in fake_files
			files.add(&f)
		return sorted(files, "path")

var lister: DBLister




func is_filter(s: string) -> bool
	return s.find("=", 0) > 0

func find_by_path_or_filter(arg: string) -> LocalFile*[]
	if is_filter(arg)
		return db.find_by_tags(arg)
	else
		return lister.find(Path(arg).absolute())
