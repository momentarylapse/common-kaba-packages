
class Matrix
	var e: float[]
	var n = 0
	var m = 0
	func __init__()
	func __init__(el: float[][])
		n = len(el) # rows
		m = len(el[0]) # columns
		e.resize(n*m)
		for i in 0:n
			for j in 0:m
				e[i*m + j] = el[i][j]
	func mut clear(_n: int, _m: int)
		n = _n
		m = _m
		e.clear()
		e.resize(n*m)
	func mut add(i: int, j: int, f: float)
		e[i * m + j] += f
	func get(i: int, j: int) -> float
		return e[i * m + j]
	func __str__() -> string
		string[] s
		for i in 0:n
			s.add(str(e[i*m:(i+1)*m]))
		return s.join("\n")
	func mut diagonalize(out b: float[])
		for i in 0:n
			float f = get(i,i)
			if f == 0
				f = 0.0000001
			#print f
			for j in i:m
				e[i * m + j] /= f
			b[i] /= f
			
			for ii in i+1:n
				float g = get(ii, i)
				for j in i:m
					e[ii * m + j] -= e[i * m + j] * g
				b[ii] -= b[i] * g
	func solve(b: float[]) -> float[]
		if n != m or len(b) != n
			raise(new Exception("size mismatch"))
		Matrix mm = self
		float[] x
		x.resize(m)
		float[] bb = b
		mm.diagonalize(bb)
		
		int i = n-1
		while i >= 0
			float s = 0
			for j in i+1:n
				s += mm.get(i, j) * x[j]
			float f = mm.get(i, i)
			if abs(f) > 0.00001
				x[i] = (bb[i] - s) # / f
			else
				x[i] = 0
			i --
		return x



# ok
func _test_solve()
	Matrix mm = Matrix([[2.,1.],[-1.,2.]])
	print(mm)
	float[] x = mm.solve([1.0,1.0])
	print("-------")
	print(x)

