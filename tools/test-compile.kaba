use os
use kaba

class Term
	const RED = "\x1b[0;31m"
	const GREEN = "\x1b[0;32m"
	const YELLOW = "\x1b[0;33m"
	const END = "\x1b[0m"

var error_count: int

func test_compile(filename: Path)
	print("    " + filename.basename())
	try
		Module.load(filename, true)
	except Exception as e
		print("{{Term.RED}}  ERROR  {{e}}{{Term.END}}")
		error_count ++

func test_rec(dir: Path)
	var subdirs = Filesystem.search(dir, "", "dr0")
	for s in subdirs
		var list = Filesystem.search(dir << s, "*.kaba", "f")
		if len(list) > 0
			print("{{Term.YELLOW}}{{dir << s}}{{Term.END}}")
		for e in list
			test_compile(dir << s << e)

func main(arg: string[])
	if len(arg) == 0
		print("directory?")
		return
	error_count = 0
	for a in arg
		test_rec(Path(a))
	if error_count > 0
		print("{{Term.RED}}{{error_count}} errors found{{Term.END}}")
	else
		print("{{Term.GREEN}}all files ok{{Term.END}}")
