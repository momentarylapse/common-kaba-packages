use os
use kaba

class Term
	const string RED = "\x1b[0;31m"
	const string GREEN = "\x1b[0;32m"
	const string YELLOW = "\x1b[0;33m"
	const string END = "\x1b[0m"

int error_count

void test_compile(Path filename)
	print("    " + filename.basename())
	try
		Script.load(filename, true)
	except Exception as e
		print("{{Term.RED}}  ERROR  {{e}}{{Term.END}}")
		error_count ++

void test_rec(Path dir)
	let list = Filesystem.search(dir, "*.kaba", true)
	if len([for e in list e if e[-4:] == "kaba"]) > 0
		print("{{Term.YELLOW}}{{dir}}{{Term.END}}")
	for e in list
		if Filesystem.is_directory(dir << e)
			test_rec(dir << e)
		else
			test_compile(dir << e)

void main(string[] arg)
	if len(arg) == 0
		print("directory?")
		return
	error_count = 0
	for a in arg
		test_rec(Path(a))
	if error_count > 0
		print("{{Term.RED}}{{error_count}} errors found{{Term.END}}")
	else
		print("{{Term.GREEN}}all files ok{{Term.END}}")