use file
use config
use resource
use language

void write_resource(File *f, Resource r)
	f << r.type
	if r.id.head(1) == "/"
		f << r.id.substr(1, -1)
	else
		f << r.id
	f << r.options.join(",")
	f << r.x
	f << r.y
	f << r.children.num
	for c in r.children
		write_resource(f, c)


void write_language(File *f, Language l)
	LangID[] ids = l.get_useful_ids()
	LangText[] texts = l.get_useful_texts()

	f << "// Language"
	f << l.name
	f << "// Number Of IDs"
	f << ids.num
	f << "// Text"
	for i in ids
		f << i.id
		f << i.title.escape()
		f << i.tooltip.escape()
	f << "// Num Language Strings"
	f << texts.num
	f << "// Text"
	for t in texts
		f << t.orig.escape()
		f << t.trans.escape()

void export_resources(Resource[] res, Language own, Language[] langs)
	File *f = Filesystem.create_text(conf.export_dir + "hui_resources.txt")
	f << "t7"
	f << "// Number Of Resources"
	f << res.num
	for r in res
		f << "// Resource"
		write_resource(f, r)
	f << "// Number Of Languages"
	f << (langs.num + 1)
	write_language(f, own)
	for l in langs
		write_language(f, l)

