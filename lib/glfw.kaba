let KABA_LINK = "/usr/lib64/libglfw.so
	_glfwInit:glfwInit
	_glfwVulkanSupported:glfwVulkanSupported
	_glfwWindowHint:glfwWindowHint
	_glfwCreateWindow:glfwCreateWindow
	_glfwWindowShouldClose:glfwWindowShouldClose
	_glfwSetWindowUserPointer:glfwSetWindowUserPointer
	_glfwGetWindowUserPointer:glfwGetWindowUserPointer
	_glfwSetKeyCallback:glfwSetKeyCallback
	_glfwSetCursorPosCallback:glfwSetCursorPosCallback
	_glfwSetMouseButtonCallback:glfwSetMouseButtonCallback
	_glfwGetCursorPos:glfwGetCursorPos
	_glfwPollEvents:glfwPollEvents
	_glfwTerminate:glfwTerminate
	_glfwJoystickPresent:glfwJoystickPresent
	_glfwGetJoystickName:glfwGetJoystickName
	_glfwJoystickIsGamepad:glfwJoystickIsGamepad
	_glfwGetGamepadName:glfwGetGamepadName
	_glfwGetGamepadState:glfwGetGamepadState
"

import hui
import time

func extern _glfwInit() -> int
func extern _glfwVulkanSupported() -> int
func extern _glfwCreateWindow(width: int, height: int, title: char*, monitor: void*, share: void*) -> void*
func extern _glfwWindowShouldClose(window: void*) -> int
func extern _glfwSetWindowUserPointer(window: void*, p: void*)
func extern _glfwGetWindowUserPointer(window: void*) -> void*
func extern _glfwSetKeyCallback(window: void*, f: void*) -> void*
func extern _glfwSetCursorPosCallback(window: void*, f: void*) -> void*
func extern _glfwSetMouseButtonCallback(window: void*, f: void*) -> void*
func extern _glfwGetCursorPos(window: void*, x: float64*, y: float64*)
func extern _glfwWindowHint(hint: int, value: int)
func extern _glfwPollEvents()
func extern _glfwTerminate()
func extern _glfwJoystickPresent(j: int) -> int
func extern _glfwJoystickIsGamepad(j: int) -> int
func extern _glfwGetJoystickName(j: int) -> char[0]*
func extern _glfwGetGamepadName(j: int) -> char[0]*
func extern _glfwGetGamepadState(j: int, p: void*) -> int

let GLFW_CLIENT_API = 0x00022001
let GLFW_NO_API = 0
let GLFW_RESIZABLE = 0x00020003

func init(opts: string[])
	_glfwInit()
	if "vulkan" in opts
		print("VULKAN!!!!")
		if _glfwVulkanSupported() == 0
			raise(new Exception("glfw: vulkan not supported"))
		_glfwWindowHint(GLFW_CLIENT_API, GLFW_NO_API)
		_glfwWindowHint(GLFW_RESIZABLE, 0)

func end()
	_glfwTerminate()

func cp2s(p: char[0]*) -> string
	if p == nil
		return ""
	var s: string
	for i in 0:256
		if p[i] == 0
			break
		s.add(p[i])
	return s

func _get_key(k: int) -> int
	if k >= 65 and k < 91
		return k - 65 + hui.KEY_A
	if k == 32
		return hui.KEY_SPACE
	if k == 256
		return hui.KEY_ESCAPE
	if k == 257
		return hui.KEY_RETURN
	if k == 258
		return hui.KEY_TAB
	if k == 259
		return hui.KEY_BACKSPACE
	if k == 262
		return hui.KEY_RIGHT
	if k == 263
		return hui.KEY_LEFT
	if k == 264
		return hui.KEY_DOWN
	if k == 265
		return hui.KEY_UP
	return -1
	

func _on_key(w: void*, a: int, b: int, c: int, d: int)
#	print("ON KEY   {{a}} {{b}} {{c}} {{d}}")
	var win: Window* = _glfwGetWindowUserPointer(w)
	var k = _get_key(a)
	if k < 0
		return
	win.input.key[k] = (c > 0)
	if c > 0
		win.on_key_down(k)

func _on_button(w: void*, a: int, action: int, c: int)
#	print("ON BUTTON   {{a}} {{action}} {{c}}")
	var win: Window* = _glfwGetWindowUserPointer(w)
	if action == 1
		if a == 0
			win.input.button[0] = true
			win.on_left_button_down()
		else if a == 1
			win.input.button[1] = true
			win.on_right_button_down()
	else if action == 0
		if a == 0
			win.input.button[0] = false
			win.on_left_button_up()
		else if a == 1
			win.input.button[1] = false
			win.on_right_button_up()

func _on_cursor(w: void*, xx: float64, yy: float64)
	var x = float(xx)
	var y = float(yy)
#	print("ON CURSOR   {{x}}  {{y}}")
	var win: Window* = _glfwGetWindowUserPointer(w)
	win.input.dx = x - win.input.x
	win.input.dy = y - win.input.y
	win.input.x = x
	win.input.y = y
	win.on_mouse_move()#x, y)


class Window
	var win: void*
	class Input
		var button: bool[3]
		var key: bool[256]
		var x, y, dx, dy: float
		func __init__()
			for b in button
				b = false
			for k in key
				k = false
	var input: Input
	func __init__(title: string, w: int, h: int)
		var _title = title + "\x00"
		win = _glfwCreateWindow(w, h, &_title[0], nil, nil)
		_glfwSetWindowUserPointer(win, &self)
		_glfwSetKeyCallback(win, raw_function_pointer(_on_key))
		_glfwSetCursorPosCallback(win, raw_function_pointer(_on_cursor))
		_glfwSetMouseButtonCallback(win, raw_function_pointer(_on_button))
	func __init__(ww: void*)
		win = ww
		_glfwSetWindowUserPointer(win, &self)
		_glfwSetKeyCallback(win, raw_function_pointer(_on_key))
		_glfwSetCursorPosCallback(win, raw_function_pointer(_on_cursor))
		_glfwSetMouseButtonCallback(win, raw_function_pointer(_on_button))
	func run()
		while _glfwWindowShouldClose(win) == 0
			_glfwPollEvents()
			on_idle()
	func virtual on_idle()
	func get_mouse() -> complex
		var xx, yy: float64
		_glfwGetCursorPos(win, &xx, &yy)
		return complex(float(xx), float(yy))
	func virtual on_key_down(key: int)
		#print("key {{key}}")
	func virtual on_mouse_move()
		#print("ON CURSOR   {{get_mouse()}}")
	func virtual on_left_button_down()
		#print("LBD")
	func virtual on_left_button_up()
		#print("LBU")
	func virtual on_right_button_down()
		#print("RBD")
	func virtual on_right_button_up()
		#print("RBU")
	func key(k: int) -> bool
		if k < 0 or k >= 256
			return false
		return input.key[k]


func main()
	init([])
	var w = new Window("test", 800, 600)
	w.run()
	print("done")
	end()

struct GamepadState
	var _buttons: bool[16]
	var axes: float[6]
	func __init__()
		for b in _buttons
			b = false
		for a in axes
			a = 0

struct Gamepad extends GamepadState as shared
	enum Button
		CROSS
		CIRCLE
		SQUARE
		TRIANGLE
		L1
		R1
		OPTIONS
		SHARE
		PS
		L3
		R3
		UP
		RIGHT
		DOWN
		LEFT
		
	var state_prev: GamepadState
	var index: int
	var deadzone: float
	func __init__(i: int)
		index = i
		deadzone = 0.05
	
	func present() -> bool
		return _glfwJoystickIsGamepad(index) == 1
	
	func mut update()
		state_prev = self
		_glfwGetGamepadState(index, &self)
		for a in axes
			if abs(a) < deadzone
				a = 0
			else if a > 0
				a = (a - deadzone) / (1 - deadzone)
			else
				a = (a + deadzone) / (1 - deadzone)
	
	func click(b: Button) -> bool
		return _buttons[int(b)] and not state_prev._buttons[int(b)]
	func button(b: Button) -> bool
		return _buttons[int(b)]
		
	
	func static get(index: int) -> shared[Gamepad]
		return new Gamepad(index)

func _main()
	init([])
	for j in 0:8
		print("{{_glfwJoystickPresent(j)}}  {{_glfwJoystickIsGamepad(j)}} {{cp2s(_glfwGetGamepadName(j))}}")
		
		
	var state: GamepadState
	while true
		time.sleep(0.1)
		_glfwGetGamepadState(0, &state)
		print(str(state))
	#end()
