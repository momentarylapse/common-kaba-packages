use os
use kaba

let REPO_FILE = ".kaba-repo"
let PACKAGE_FILE = ".kaba-package"

struct Source
	type: string
	name: string
	url: string
	
	func base_path() -> os.Path
		if type == "repo"
			return conf.repos_base | name
		if type == "local"
			return url
		return ""

enum Location
	INTERNAL
	INSTALLED
	REMOTE

struct Package
	name: string
	version: string
	base_installed: os.Path
	base_source: os.Path # location of the source package
	root_source: os.Path # actual root of files to copy in source (might be child of base_source)
	build_mode: string?
	build_dir: string
	build_target: string
	location: Location
	requires: string[]
	func mut __init__() # kaba ARM workaround...
	

class Config
	var kaba_base: os.Path
	var packages_base: os.Path
	var repos_base: os.Path
	var sources: Source[]
	var packages_internal: Package[]
	var packages_installed: Package[]
	var packages_remote: Package[]
	var verbose = false
var conf: Config


func load_basic_config()
	conf.kaba_base = os.app_directory_dynamic
	conf.packages_base = conf.kaba_base | "packages"
	conf.repos_base = conf.kaba_base | "repos"
	
	if not os.fs.is_directory(conf.packages_base)
		os.fs.create_directory(conf.packages_base)
	if not os.fs.is_directory(conf.repos_base)
		os.fs.create_directory(conf.repos_base)
	
	if os.fs.exists(conf.kaba_base | "sources")
		let s = os.fs.read_text(conf.kaba_base | "sources")
		for l in s.explode("\n")
			let x = l.replace("\t", " ").explode(" ")
			if len(x) >= 3
				conf.sources.add([x[0], x[1].replace("/", "."), x[2]])


func find_internal_packages() -> Package[]
	var r: Package[]
	for p in kaba.default_context.packages
		var pp: Package
		pp.name = p.name
		pp.version = "0" # TODO
		pp.location = Location.INTERNAL
		r.add(pp)
	return r


func read_package(name: string, dir: os.Path, location: Location) -> Package
	var p: Package
	p.name = name
	p.version = "0"
	
	p.base_installed = conf.packages_base | name
	if location == Location.INSTALLED
		p.base_installed = dir
	else
		p.base_source = dir
		p.root_source = dir
		
	if os.fs.exists(dir | PACKAGE_FILE)
		let ss = os.fs.read_text(dir | PACKAGE_FILE)
		for l in ss.explode("\n")
			let x = l.explode(" ")
			if len(x) < 2
				continue
			if x[0] == "version"
				p.version = x[1]
			else if x[0] == "requires"
				p.requires.add(x[1])
			else if x[0] == "root"
				p.root_source = p.base_source | x[1]
			else if x[0] == "build" and len(x) >= 4
				p.build_mode = x[1]
				p.build_dir = x[2]
				p.build_target = x[3]
	p.location = location
	return p


func find_remote_packages() -> Package[]
	var r: Package[]
	for s in conf.sources
		let base = s.base_path()
		if base == ""
			continue
		if not os.fs.exists(base | REPO_FILE)
			continue
		let ss = os.fs.read_text(base | REPO_FILE)
		for l in ss.explode("\n")
			let x = l.explode(" ")
			if len(x) < 2
				continue
			if x[0] != "package"
				continue
			var name = os.Path(x[1]).basename().replace(".kaba", "")
			let p = read_package(name, base | x[1], Location.REMOTE)
			for pp in list_find(r, name)
				print("   MULTIPLE SOURCES FOR {{name}}:  {{p.base_source}}  {{pp.base_source}}")
			else
				r.add(p)
	return r


func find_installed_packages() -> Package[]
	var r: Package[]
	let list = os.fs.search(conf.packages_base, "*", "d")
	for e in list
		r.add(read_package(str(e), conf.packages_base | e, Location.INSTALLED))
	return r


func list_find(list: Package[], name: string) -> Package*
	for p in list
		if p.name == name
			return &p
	return nil


func file_equal(a: os.Path, b: os.Path) -> bool
	if not os.fs.exists(a) or not os.fs.exists(b)
		return false
	let sa = os.fs.read(a)
	let sb = os.fs.read(b)
	return sa == sb


func load_module_list()
	conf.packages_internal = find_internal_packages()
	conf.packages_installed = find_installed_packages()
	conf.packages_remote = find_remote_packages()


func action_update(args: string[])
	for s in conf.sources
		if s.type != "repo"
			continue
		
		print("updating package {{s.name}}")
		let base = conf.repos_base | s.name
		if not os.fs.is_directory(base)
			os.fs.create_directory(base)
		let git = base | ".git" # FIXME bug in kaba!!!!
		if not os.fs.is_directory(base | ".git")
			let r = os.shell_execute("cd {{base}}; git clone {{s.url}} .")
		let r = os.shell_execute("cd {{base}}; git pull --rebase")


func action_upgrade(args: string[])
	action_update(args)
	load_module_list()
	
	var needs_upgrade: string[]
	for p in conf.packages_installed
		for pp in list_find(conf.packages_remote, p.name)
			if pp.version != p.version
				needs_upgrade.add(p.name)
	
	for name in needs_upgrade
		for p in list_find(conf.packages_remote, name)
			install_package(p)


func is_installed_latest(name: string) -> bool
	for p in list_find(conf.packages_internal, name)
		return true
	for p in list_find(conf.packages_installed, name)
		for pp in list_find(conf.packages_remote, p.name)
			return pp.version == p.version
		# no remote? ...not sure
		return true
	# not installed
	return false


func action_list(args: string[])
	load_module_list()
	
	var packages = conf.packages_installed
	for p in conf.packages_internal
		packages.add(p)
	for p in conf.packages_remote
		for pp in list_find(conf.packages_installed, p.name)
			pass
		else
			packages.add(p)

	for p in packages |> sort("name")
		var s = ""
		if p.location == Location.INTERNAL
			s = "{{os.terminal.GRAY}}internal{{os.terminal.END}}"
		if p.location == Location.INSTALLED
			s = "{{os.terminal.GREEN}}installed{{os.terminal.END}}"
			for pp in list_find(conf.packages_remote, p.name)
				if pp.version != p.version
					s = "{{os.terminal.RED}}installed, version {{p.version}}, source version {{pp.version}}{{os.terminal.END}}"
			else
				s = "{{os.terminal.RED}}installed, but no source{{os.terminal.END}}"
			for r in p.requires
				if not is_installed_latest(r)
					s += " {{os.terminal.RED}}requires {{r}}{{os.terminal.END}}"
		if conf.verbose
			s = "{{p.version|3}} {{s}}"
		print("{{p.name|16}}  {{s}}")
		if conf.verbose and len(p.requires) > 0
			print("                      {{os.terminal.GRAY}}requires:{{os.terminal.END}} " + p.requires.join(", "))


func delete_directory(dir: os.Path)
#	let list = os.fs.search(dir, "*", "rf")
#	for e in list
#		let f = dir | e
#		#if conf.verbose
#		#	print("{{os.terminal.YELLOW}}deleting{{os.terminal.END}} {{f}}")
#		os.fs.delete(f)
#	let listd = os.fs.search(dir, "*", "rd")
#	print(listd)
#	for e in listd
#		let f = dir | e
#		#if conf.verbose
#		#	print("{{os.terminal.YELLOW}}deleting{{os.terminal.END}} {{f}}")
#		os.fs.delete_directory(f)
#	os.fs.delete_directory(dir)
	let r = os.shell_execute("rm -rf '{{dir}}'")

func remove_package(p: Package)
	print("{{os.terminal.YELLOW}}deleting{{os.terminal.END}} {{p.base_installed}}")
	delete_directory(p.base_installed)


func allow_install_file(f: os.Path) -> bool
	let pp = f.all_parents()
	var b = str(f)
	if len(pp) > 0
		b = str(pp[len(pp) - 1]).replace("/", "")
	#print("{{f}}  {{b}}")
	if b in ["src", "build", "CMakeLists.txt", "CMakePresets.json", ".vs", "xmake.conf", "vcpkg-configuration.json", "vcpkg.json", "sharedlib"]
		return false
	
	return true

func install_package(p: Package) -> bool
	for r in p.requires
		if not is_installed_latest(r)
			for rr in list_find(conf.packages_remote, r)
				if not install_package(rr)
					return false
			else
				print("{{os.terminal.RED}}MISSING DEPENDENCY{{os.terminal.END}} {{p.name}} -> {{r}}")
				return false

	print("{{os.terminal.YELLOW}}installing{{os.terminal.END}} {{p.base_installed}}")
	if os.fs.is_directory(p.base_installed)
		delete_directory(p.base_installed)
	os.fs.create_directory(p.base_installed)


	# build shared library?
	for m in p.build_mode
		let build_dir = os.Path("/tmp/kaba-build-{{p.name}}")
		if os.fs.is_directory(build_dir)
			delete_directory(build_dir)
		os.fs.create_directory(build_dir)
		print("build...")
		let source_dir = p.base_source | p.build_dir
		try
			let r = os.shell_execute("cd '{{build_dir}}'; cmake {{source_dir}} -GNinja && ninja {{p.build_target}} && cp libmodule* '{{p.base_installed}}'", true)
		except
			print("{{os.terminal.RED}}ERROR{{os.terminal.END}} failed building")
			delete_directory(build_dir)
			delete_directory(p.base_installed)
			return false
		delete_directory(build_dir)


	# copy files
	let listd = os.fs.search(p.root_source, "*", "rd")
	for e in listd
		if allow_install_file(e)
			os.fs.create_directory(p.base_installed | e)
		
	let list = os.fs.search(p.root_source, "*", "rf")
	for e in list
		if allow_install_file(e)
			let a = p.root_source | e
			let b = p.base_installed | e
			if conf.verbose
				print("{{os.terminal.YELLOW}}copy{{os.terminal.END}} {{a}}  ->  {{b}}")
			os.fs.copy(a, b)
	
	# package info file might be skipped if root!=base...
	if os.fs.exists(p.base_source | PACKAGE_FILE)
		os.fs.copy(p.base_source | PACKAGE_FILE, p.base_installed | PACKAGE_FILE)
	return true


func action_install(args: string[])
	load_module_list()
	for name in args
		for p in list_find(conf.packages_internal, name)
			print("{{os.terminal.RED}}internal packages can not be installed{{os.terminal.END}}")
			return
				
		for p in list_find(conf.packages_remote, name)
			install_package(p)
		else
			print("{{os.terminal.RED}}package not found{{os.terminal.END}}")


func action_remove(args: string[])
	load_module_list()
	for name in args
		for p in list_find(conf.packages_internal, name)
			print("{{os.terminal.RED}}internal packages can not be removed{{os.terminal.END}}")
			return
		
		for p in list_find(conf.packages_installed, name)
			remove_package(p)
		else
			print("{{os.terminal.RED}}package not found{{os.terminal.END}}")


func main(args: string[])
	load_basic_config()

	var p: os.CommandLineParser
	p.info("package", "kaba package manager")
	p.option("-v", "verbose", func() conf.verbose = true)
	
	p.cmd("update", "", "update database / repositories", action_update)
	p.cmd("upgrade", "", "update all outdated packages", action_upgrade)
	p.cmd("list", "", "list available packages", action_list)
	p.cmd("install", "NAME", "install package", action_install)
	p.cmd("remove", "NAME", "remove package", action_remove)
	p.parse(args)
