use "circuit.kaba"
use "panel.kaba"

class CircuitEditor extends Window
	Circuit c
	CircuitPanel* panel
	
	
	void __init__(string[] arg)
		super.__init__("Circuit Editor", 800, 600)
		from_source("
Grid root ''
	.
	Grid ? '' width=250 vertical noexpandx
		Group ? 'Add'
			ListView add-type 'what' nobar height=200 noexpandy
		Group ? 'Part'
			ListView params 'name\\\\value' nobar format=tT height=120 noexpandy expandx
		Group ? 'Simulation'
			Grid ? '' vertical
				Grid ? ''
					Text ? 'time'
					SpinButton t '5.0' range=0:100:0.1 expandx
					---|
					Text ? 'dt'
					SpinButton dt '0.01' range=0:1:0.001
					---|
					Text ? 'I input'
					SpinButton input-amp '1' range=0:100:0.001
					---|
					Text ? 'f input'
					SpinButton input-freq '1' range=0:100:0.001
					---|
					Text ? 'type'
					ComboBox input-type '⎻\\\\∿\\\\⎍\\\\◢'
				Button simulate 'Run' expandx")
		panel = new CircuitPanel(&c)
		embed(panel, "root", 0,0)
		toolbar(0).from_source("
Toolbar toolbar ''
	Item new 'New' image=hui:new
	Item open 'Open' image=hui:open
	Item save 'Save' image=hui:save
	Separator
	Item simulate 'Run' image=hui:media-play")
		
		set_maximized(true)
		
		set_string("add-type", "Node")
		set_string("add-type", "Cable")
		set_string("add-type", "Resistor")
		set_string("add-type", "Capacitor")
		set_string("add-type", "Inductor")
		set_string("add-type", "Diode")
		
		if len(arg) > 0
			c.import(FileReadText(arg[0]))
		
		event("new", &on_new)
		event("open", &on_open)
		event("save", &on_save)
		event("simulate", &on_simulate)
		event("add-type", &on_add_type)
		event_x("params", "hui:change", &on_param)
		load_from_part(nil)
	
	void on_new()
		panel.res.clear()
		c.reset_default()
		panel.redraw()
	
	void on_save()
		if HuiFileDialogSave(self, "Save the circuit", "", "*.circuit", "*.circuit")
			FileWriteText(HuiFilename, c.export())
	
	void on_open()
		if HuiFileDialogOpen(self, "Load the circuit", "", "*.circuit", "*.circuit")
			c.import(FileReadText(HuiFilename))
	
	void on_param()
		int row = HuiGetEvent().row
		string p = get_cell("params", row, 1)
		if panel.sel.type == SEL_TYPE_EDGE
			c.edges[panel.sel.index].param[row] = p.float()
	
	void load_from_part(Edge *e)
		reset("params")
		if !e
			return
		string[] names = e.param_names()
		for n,i in names
			add_string("params", n + "\\" + e.param[i])
			
		

		
	void on_simulate()
		float duration = get_float("t")
		float dt = get_float("dt")
		float I = get_float("input-amp")
		float f = get_float("input-freq")
		int type = get_int("input-type")
		panel.res.clear()
		c.reset_state()
		c.simulate(duration, dt, I, f*2*pi, type, panel.res)
		panel.auto_scale()
		panel.redraw()
	
	
	void on_add_type()
		int t = get_int("")
		panel.mode = MODE_ADD_EDGE
		panel.first_node = -1
		if t == 0
			panel.mode = MODE_ADD_NODE
		else if t == 1
			panel.add_type = TYPE_CABLE
		else if t == 2
			panel.add_type = TYPE_RESISTOR
		else if t == 3
			panel.add_type = TYPE_CAPACITOR
		else if t == 4
			panel.add_type = TYPE_INDUCTOR
		else if t == 5
			panel.add_type = TYPE_DIODE
		panel.redraw()

void main(string[] arg)
	Window* w = new CircuitEditor(arg)
	w.run()
