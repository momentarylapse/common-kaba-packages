use os
use hui


class Term
	const RED = "\x1b[0;31m"
	const GREEN = "\x1b[0;32m"
	const YELLOW = "\x1b[0;33m"
	const BLUE = "\x1b[0;34m"
	const BLUE2 = "\x1b[0;36m"
	const VIOLET = "\x1b[0;35m"
	const BOLD = "\x1b[1m"
	const END = "\x1b[0m"


class Profile
	var name: string
	var libs_optional: string[]

# project
class Config
	var root: Path
	var verbose = false
	var dry_run = false
	var force_all_libraries = false
	var force_rebuild = false
	var force_profile: string
	
	var name: string
	var binary_file: Path
	var source_dir: Path
	var main_file: string
	var libs: string[]
	var libs_optional: string[]
	var ignore_libs: string[]
	var profiles: Profile[]
	var language = "c++17"
	var optimization = "2"
	var build_type = "debug"
	var flags_global: string
	var flags_compiler: string
	var flags_linker: string
	var icons: string[]
	var desktop_file: Path
	var mime_files: string[]
	var install_source: Path
	var installs_additional: string[]
	var outputs = ["make"]
	
	func ignored(l: string) -> bool
		for ig in ignore_libs
			if l == ig
				return true
		return false
	
	func load()
		if not Filesystem.exists(conf.root << "xmake.conf")
			raise(new Exception("no xmake.conf found"))
		var c: hui.Configuration
		c.load(conf.root << "xmake.conf")
		install_source = "static"
		for k in c.keys()
			if k in ["project.name"]
				name = c[k]
			else if k in ["build.source"]
				source_dir = c[k]
			else if k in ["build.binary"]
				binary_file = c[k]
			else if k == "build.main"
				main_file = c[k]
			else if k == "build.language"
				language = c.get_str(k, "c++17")
			else if k == "build.flags"
				flags_global = c[k]
			else if k == "build.optimization"
				optimization = c.get_str(k, "2")
			else if k == "build.type"
				build_type = c.get_str(k, "debug")
			#else if k == "compiler-flags"
#				flags_compiler = c[k]
#			else if k == "linker-flags"
#				flags_linker = c[k]
			else if k == "build.libraries"
				parse_libs(c[k], libs, libs_optional)
			#else if k == "build.libraries-optional"
			#	libs_optional = c[k].explode(",")
			else if k == "install.source"
				install_source = c[k]
			else if k == "install.icons"
				icons = c[k].explode(",")
			else if k == "install.desktop-file"
				desktop_file = c[k]
			else if k == "install.mime-files"
				mime_files = c[k].explode(",")
			else if k == "install.additional"
				installs_additional = c[k].explode(",")
			else if k[:14] == "build.profile-"
				Profile p
				p.name = k[14:]
				p.libs_optional = c[k].explode(",")
				profiles.add(p)
			else if k == "build.system"
				outputs = c[k].explode(",")
			else
				raise(new Exception("unknown tag: {{k}}"))
	func save()
		var c: hui.Configuration
		c["project.name"] = name
		c["build.source"] = source_dir
		c["build.binary"] = binary_file
		c["build.main"] = main_file
		c["build.language"] = language
		c["build.optimization"] = optimization
		c["build-type"] = build_type
		c["build.flags"] = flags_global
		c["build.libraries"] = libs.join(",")
		c["build.libraries-optional"] = libs_optional.join(",")
		for p in profiles
			c["profile-{{p.name}}"] = p.libs_optional.join(",")
		c["install.source"] = install_source
		c["install.icons"] = icons.join(",")
		c["install.desktop-file"] = desktop_file
		c["install.mime-files"] = mime_files.join(",")
		c["install.additional"] = installs_additional.join(",")
		c.save(conf.root << "xmake.conf")
	
	func needs_installing() -> Path[]
		var r: Path[]
		var list = Filesystem.search(install_source, "*.*", "fd")
		var ignore = mime_files
		ignore.add(desktop_file)
		for e in list
			if not (str(install_source << e) in ignore)
				r.add(install_source << e)
		for i in installs_additional
			r.add(i)
		return r
		
	func has_explicit_default_profile() -> bool
		for p in conf.profiles
			if p.name == "default"
				return true
		return false

var conf: Config

func parse_libs(s: string, out libs: string[], out libs_opt: string[])
	for x in s.explode(",")
		if x[-1:] == "?"
			libs_opt.add(x[:-1])
		else
			libs.add(x)

class SourceFile
	var filename: Path
	func const object() -> Path
		return Path(str(filename).replace(".cpp", ".o"))
	var dep: Path[]
	func add_dep(d: Path) -> bool
		if d in dep
			return false
		dep.add(d)
		return true

var sources: SourceFile[]

func rel(filename: Path) -> Path
	return filename.relative_to(conf.root)

func relsrc(filename: Path) -> Path
	return filename.relative_to(conf.root << conf.source_dir)


func find_dep(out f: SourceFile, filename: Path)
	string s
	try
		s = Filesystem.read(filename)
	except
		return
	var lines = s.explode("\n")
	
	for l in lines
		int p = l.find("#include \"", 0)
		if p >= 0
			int q = l.find("\"", p + 11)
			Path d = filename.parent() << l[p+10:q]
			d = d.canonical()
			if Filesystem.exists(d)
				if f.add_dep(d)
				#	print("  <" + rel(d) + ">")
					find_dep(f, d)

func parse_source(filename: Path)
	if conf.verbose
		print(rel(filename))
	SourceFile f
	f.filename = filename
	
	find_dep(f, filename)
	
	sources.add(f)

func parse_dir(dir: Path)
	var files = Filesystem.search(dir, "*.cpp", "fr")
	for f in files
		parse_source(dir << f)
