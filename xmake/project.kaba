use os
use hui


class Term
	const RED = "\x1b[0;31m"
	const GREEN = "\x1b[0;32m"
	const YELLOW = "\x1b[0;33m"
	const BLUE = "\x1b[0;34m"
	const BLUE2 = "\x1b[0;36m"
	const VIOLET = "\x1b[0;35m"
	const BOLD = "\x1b[1m"
	const END = "\x1b[0m"


class Profile
	var name: string
	var libs_optional: string[]

# project
class Config
	var root: Path
	var verbose = false
	var dry_run = false
	var force_all_libraries = false
	var force_rebuild = false
	var force_profile: string
	
	var name: string
	var binary_file: string
	var main_file: string
	var libs: string[]
	var libs_optional: string[]
	var ignore_libs: string[]
	var profiles: Profile[]
	var language = "c++17"
	var optimization = "2"
	var build_type = "debug"
	var flags_global: string
	var flags_compiler: string
	var flags_linker: string
	var icons: string[]
	var desktop_file: string
	var mime_files: string[]
	var installs: string[]
	var outputs = ["make"]
	
	func ignored(l: string) -> bool
		for ig in ignore_libs
			if l == ig
				return true
		return false
	
	func load()
		if not Filesystem.exists(conf.root << "xmake.conf")
			raise(new Exception("no xmake.conf found"))
		var c: hui.Configuration
		c.load(conf.root << "xmake.conf")
		for k in c.keys()
			if k in ["project", "project-name"]
				name = c[k]
			else if k in ["binary-name", "binary", "binary-file"]
				binary_file = c[k]
			else if k == "main-file"
				main_file = c[k]
			else if k == "language"
				language = c.get_str(k, "c++17")
			else if k == "flags"
				flags_global = c[k]
			else if k == "optimization"
				optimization = c.get_str(k, "2")
			else if k == "build-type"
				build_type = c.get_str(k, "debug")
			#else if k == "compiler-flags"
#				flags_compiler = c[k]
#			else if k == "linker-flags"
#				flags_linker = c[k]
			else if k == "libraries"
				libs = c[k].explode(",")
			else if k == "libraries-optional"
				libs_optional = c[k].explode(",")
			else if k == "icons"
				icons = c[k].explode(",")
			else if k == "desktop-file"
				desktop_file = c[k]
			else if k == "mime-files"
				mime_files = c[k].explode(",")
			else if k == "additional-install"
				installs = c[k].explode(",")
			else if k[:8] == "profile-"
				Profile p
				p.name = k[8:]
				p.libs_optional = c[k].explode(",")
				profiles.add(p)
			else if k == "output"
				outputs = c[k].explode(",")
			else
				raise(new Exception("unknown tag: {{k}}"))
	func save()
		var c: hui.Configuration
		c["project"] = name
		c["binary"] = binary_file
		c["main-file"] = main_file
		c["language"] = language
		c["optimization"] = optimization
		c["build-type"] = build_type
		c["flags"] = flags_global
		c["libraries"] = libs.join(",")
		c["libraries-optional"] = libs_optional.join(",")
		for p in profiles
			c["profile-{{p.name}}"] = p.libs_optional.join(",")
		c["icons"] = icons.join(",")
		c["desktop-file"] = desktop_file
		c["mime-files"] = mime_files.join(",")
		c["additional-install"] = installs.join(",")
		c.save(conf.root << "xmake.conf")
	
	func needs_installing() -> Path[]
		var r: Path[]
		var list = Filesystem.search(Path("../static"), "*.*", "fd")
		var ignore = mime_files
		ignore.add(desktop_file)
		for e in list
			if not (str(e) in ignore)
				r.add(Path("static") << e)
		for i in installs
			r.add(i)
		return r
		
	func has_explicit_default_profile() -> bool
		for p in conf.profiles
			if p.name == "default"
				return true
		return false

var conf: Config

class SourceFile
	var filename: Path
	func const object() -> Path
		return Path(str(filename).replace(".cpp", ".o"))
	var dep: Path[]
	func add_dep(d: Path) -> bool
		if d in dep
			return false
		dep.add(d)
		return true

var sources: SourceFile[]

func rel(filename: Path) -> Path
	return filename.relative_to(conf.root)


func find_dep(out f: SourceFile, filename: Path)
	string s
	try
		s = Filesystem.read(filename)
	except
		return
	var lines = s.explode("\n")
	
	for l in lines
		int p = l.find("#include \"", 0)
		if p >= 0
			int q = l.find("\"", p + 11)
			Path d = filename.parent() << l[p+10:q]
			d = d.canonical()
			if Filesystem.exists(d)
				if f.add_dep(d)
				#	print("  <" + rel(d) + ">")
					find_dep(f, d)

func parse_source(filename: Path)
	if conf.verbose
		print(rel(filename))
	SourceFile f
	f.filename = filename
	
	find_dep(f, filename)
	
	sources.add(f)

func parse_dir(dir: Path)
	var files = Filesystem.search(dir, "*.cpp", "fr")
	for f in files
		parse_source(dir << f)
