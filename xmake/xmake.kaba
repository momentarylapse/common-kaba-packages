use .library.*
use .project.*
use .makefile.*
use .visualstudio
use .meson
use .cmake
use .vcpkg
use .build.*
use os


func action_has(a: string[])
	lib.test(a[0])

func action_list(a: string[])
	for l in sort(lib.libraries, "name")
		if l.test()
			print("{{l.name|14}} {{os.terminal.GREEN}}found{{os.terminal.END}}")
		else
			print("{{l.name|14}} {{os.terminal.RED}}not found{{os.terminal.END}}")

func action_init(a: string[])
	conf.save()

func action_update(a: string[])
	print("updating {{conf.root}}")
	
	conf.load()
	
	var missing = []#conf.libs |> filter(l => not lib.test(l))
	let lib_groups = conf.dependencies()
	for g in lib_groups
		if g.optional
			for l in g.libs
				if not conf.ignored(l)
					lib.test(l)
	if len(missing) > 0
		print("ERROR: necessary libraries missing: {{missing}}")
	
	conf._find_sources()

	if not conf.dry_run
		if "make" in conf.build.systems
			create_all_makefiles()
		if "meson" in conf.build.systems
			meson.create()
		if "vs2019" in conf.build.systems
			visualstudio.create_visual_studio(2019)
		if "cmake" in conf.build.systems
			cmake.create()
		if "cmake+vs2022" in conf.build.systems
			raise(new Exception("cmake+vs2022 deprecated. Use cmake + features=[vs2022]"))

func action_build(a: string[])
	#print("building {{conf.root}}")
	conf.load()
	conf._find_sources()
	if not build()
		os.exit(1)

func action_install(a: string[])
	#print("building {{conf.root}}")
	conf.load()
	conf._find_sources()
	if not build()
		os.exit(1)
	if not install()
		os.exit(1)

func action_clear(a: string[])
	#print("clearing {{conf.root}}")
	conf.load()
	conf._find_sources()
	build_clear()


#func action_vcpkg_install(a: string[])
#	conf.load()
#	
#	var req: string[]
#	
#	for g in conf.lib_groups
#		if g.optional
#			for l in g.libs
#				if !conf.ignored(l)
#					req += lib.get(l).vcpkg
#		else
#			for l in g.libs
#				req += lib.get(l).vcpkg
#	req = sort(req, "")
#	if len(req) == 0
#		return
#
#	var cmd = "vcpkg install"
#	for r in req
#		cmd += " {{r}}:x64-windows"
#	print(cmd)

func action_create_vcpkg(a: string[])
	conf.load()
	vcpkg.create()


func main(arg: string[])
	lib.init()
	conf.verbose = false
	conf.detect_project()
	conf.detect_environment()
	
	#conf.ignore_libs
	
	#bool build
	
	var p: os.CommandLineParser
	var pp = &p
	p.info("xmake", "the build tool for lazy people")
	p.option("-v/--verbose", "verbose output", func() conf.verbose = true)
	p.option("--dry", "dry run", func() conf.dry_run = true)
###	p.option("--ignore-lib", "LIB", "", func(o: string) conf.ignore_libs.add(o))
	p.option("--all", "enable all libraries", func() conf.force_all_libraries = true)
	p.option("--rebuild", "clear before build", func() conf.force_rebuild = true)
	p.option("--profile", "PROFILE", "", func(o: string) conf.force_profile = o)
	p.option("-j/--threads", "THREADS", "number of build threads", func(o: string) conf.force_threads = int(o))
	p.option("--build-dir", "BUILD_DIR", "", func(o: string) conf.force_build_dir = o)
	p.option("--prefix", "INSTALL_PREFIX_DIR", "", func(o: string) conf.install_prefix = o)
	
	p.cmd("--help", "", "show help", func(a: string[]) pp.show())
	p.cmd("lib has", "LIBRARY", "check if available", action_has)
	p.cmd("lib list", "", "list available libs", action_list)
	p.cmd("init", "", "create new project", action_init)
	p.cmd("build", "", "build project", action_build)
	p.cmd("install", "", "build and install project", action_install)
	p.cmd("clear", "", "clear build files", action_clear)
	p.cmd("update", "", "update build files", action_update)
#	p.cmd("vcpkg-install", "", "print vcpkg install command", action_vcpkg_install)
	p.cmd("vcpkg", "", "create vcpkg config files", action_create_vcpkg)
	p.cmd("", "", "default: build", action_build)
	p.parse(arg)
