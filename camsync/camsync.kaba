use os.*


struct Camera
	name: string
	source: Path
	dest: Path


func make_all_dirs(path: Path)
#	var parents = path.all_parents()
#	print(path.all_parents())
	var parts = str(path).explode("/")
	var dir: string
	for i=>p in parts
		if i == len(parts) - 1
			break
		if i > 0
			dir += "/"
		dir += p
		if i > 0
			if fs.exists(Path(dir))
				continue
			try
				fs.create_directory(Path(dir))
			except Exception as e
				error_out(e)
				return


func log_ok(op: string, msg: string)
	#{{terminal.WHITE}}
	print("{{terminal.GREEN}}{{terminal.BOLD}}{{op.upper()}}{{terminal.END}}  {{msg}}")

func log_warn(op: string, msg: string)
	print("{{terminal.YELLOW}}{{terminal.BOLD}}{{op.upper()}}{{terminal.END}}  {{msg}}")

func log_error(op: string, msg: string)
	print("{{terminal.RED}}{{terminal.BOLD}}{{op.upper()}}{{terminal.END}}  {{msg}}")

func error_out(e: Exception*)
	log_error("error", str(e))


func load_config() -> Camera[]
	make_all_dirs(app_directory_dynamic | "camsync")
	let path = app_directory_dynamic | "camsync/config"
	if not fs.exists(path)
		var c: Configuration
		c["example.source"] = ""
		c["example.destination"] = ""
		c.save(path)
	
	var c: Configuration
	c.load(path)
	
	var cameras: Camera[]
	for k in c.keys() |> filter(x => (x.tail(7) == ".source"))
		let name = k[:-7]
		#print(name)
		var cam: Camera
		cam.name = name
		cam.source = str(c[name + ".source"])
		cam.dest = str(c[name + ".destination"])
		cameras.add(cam)
	return cameras
	

func main()
	let cameras = load_config()
	#print(cameras)

	for cam in cameras
		if not fs.exists(cam.source)
			print("{{terminal.YELLOW}}camera {{cam.name}} not mounted{{terminal.END}}")
			continue
			
		print("== camera: {{cam.name}} ==")
		let path_imported = app_directory_dynamic | "camsync" | "{{cam.name}}-imported"

		var imported = fs.read_text(path_imported).explode("\n")

		var nall = 0
		var nskip = 0
		for f in fs.search(cam.source, "*", "f")
			#print(f)
			nall ++
	
			if str(f) in imported
				nskip ++
				continue
				
			print(f)
			try
				fs.copy(cam.source | f, cam.dest | f)
				imported.add(str(f))
				fs.write_text(path_imported, 	imported.join("\n"))
			except Exception as e
				print("    FAIL")

		print("{{nall}} files, {{nskip}} skipped")
