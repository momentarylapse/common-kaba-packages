const string SERVER = "michi.is-a-geek.org"
const string YII = "/var/www/html/_alexandria4/yii"
const string TEMP_OUT = "/tmp/sisyphos_out"



	
string get_tag(string[] parts, string key)
	for p in parts
		if p.find("=", 0) < 0
			continue
		string[] kv = p.explode("=")
		if kv[0] == key
			return kv[1]
	return ""

class ConfigDir
	string dir
	ConfigDirSync[] syncs
	
	void add_sync(string filter, string options)
		ConfigDirSync s
		s.dir = dir
		s.filter = filter
		s.options = options
		syncs.add(s)

class ConfigDirSync
	string dir
	string filter
	string options
	


	string[] get_structure()
		string[] oo = options.explode(";")
		for o in oo
			if o.substr(0,10) == "structure="
				return o.substr(10,-1).explode("/")

	string guess_dir(string[] parts)
		string[] structure = get_structure()
		string dir
		for s in structure
			string v = get_tag(parts, s)
			if v.num > 0
				dir += v + "/"
		return dir


class Config
	void __init__()
		verbose = false
		allow_remote_pull = false
		allow_remote_push = false
		allow_local_search = true
		allow_remote_search = true
		show_pure_local = false
		drop_deleted = false
		pull_tags = false

	void load()
		File *f = FileOpen("sisyphos.conf")
		int n, m
		f >> n
		for i in 0:n
			ConfigDir e
			f >> e.dir
			f >> m
			for j in 0:m
				ConfigDirSync s
				s.dir = e.dir
				f >> s.filter
				f >> s.options
				e.syncs.add(s)
			dirs.add(e)
		f >> n
		for i in 0:n
			string ig
			f >> ig
			ignore.add(ig)
		delete f
	void save()
		File *f = FileCreate("sisyphos.conf")
		f << dirs.num
		for e in dirs
			f << e.dir
			f << e.syncs.num
			for s in e.syncs
				f << s.filter
				f << s.options
		f << ignore.num
		for i in ignore
			f << i
		delete f

	void show()
		for e in conf.dirs
			print(e.dir)
			for s in e.syncs
				print("  filter: " + s.filter)
				print("    options: " + s.options)

	void add_filter(string dir, string filter, string options)
		for e in dirs
			if e.dir == dir
				e.add_sync(filter, options)
				return
		ConfigDir e
		e.dir = dir
		e.add_sync(filter, options)
		dirs.add(e)
	
	bool dir_is_in_db(string dir)
		for e in dirs
			if e.dir == dir.head(e.dir.num)
				return true
		return false

	void rm_filter(string dir, string filter)
		for e in dirs
			if e.dir == dir
				for s,i in e.syncs
					if s.filter == filter
						e.syncs.remove(i)
						return
		print("not found...")

	void rm_dir(string dir)
		for e,i in dirs
			if e.dir == dir
				dirs.remove(i)
				return
		print("not found...")
		
	ConfigDir[] dirs
	string[] ignore
	
	// runtime
	bool verbose
	bool allow_remote_pull
	bool allow_remote_push
	bool allow_local_search
	bool allow_remote_search
	bool show_pure_local
	bool drop_deleted
	bool pull_tags

Config conf
