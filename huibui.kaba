use "config.kaba"

class Resource
	string type
	string id
	string title
	string tooltip
	string[] options
	bool enabled
	int x, y, w, h
	int page
	string image
	Resource[] children
	//void reset()
	extern void show(int indent)
	extern string str(int indent)
	
extern Resource ParseResource(string buffer, bool literally)

class DemoDialog extends Dialog
	void __init__(string source)
		super.__init__("", 100, 100, nil, false)
		setBorderWidth(10)
		fromSource(source)
		event("close", &destroy)

void show(string filename)
	string source = FileRead(filename)
	//print(source)
	Resource r = ParseResource(source, true)
	//r.show(0)
	//print(r.str(0))
	
	if r.type == "Dialog"
		Dialog* dlg = new DemoDialog(source)
		dlg.run()
	else if r.type == "Menu"
		print("don't know how to show a menu")
	else
		print("don't know how to show a " + r.type)



class LangID
	string id
	string title
	string tooltip

class LangText
	string orig
	string trans

class Language
	string name
	LangID[] ids
	LangText[] texts
	void show()
		print(name)
		for i in ids
			print(i.id)
			print("\t" + i.title)
			print("\t" + i.tooltip)
			
		for t in texts
			print(t.orig)
			print("\t" + t.trans)

void lang_id_add(LangID[] ids, string id, string title, string tooltip, string ns)
	if id == "?" or id == ""
		return
	string cid = id
	if cid.find("/", 0) < 0
		//if ns != id
		cid = ns + "/" + id
	for i in ids
		if i.id == cid
			return
	LangID a
	a.id = cid
	a.title = title
	a.tooltip = tooltip
	ids.add(a)
	

void get_ids(Resource r, LangID[] ids, string ns)
	lang_id_add(ids, r.id, r.title, r.tooltip, ns)
	
	for c in r.children
		get_ids(c, ids, ns)

void sort_ids(LangID[] ids)
	for i in 0:ids.num
		for j in i+1:ids.num
			if ids[i].id > ids[j].id
				LangID t = ids[i]
				ids[i] = ids[j]
				ids[j] = t

void sort_texts(LangText[] texts)
	for i in 0:texts.num
		for j in i+1:texts.num
			if texts[i].orig > texts[j].orig
				LangText t = texts[i]
				texts[i] = texts[j]
				texts[j] = t

void find_all_ids(Language lang)
	DirEntry[] files = DirSearch(conf.dir, "*.hui", false)
	for f in files
		string source = FileRead(conf.dir + f.name)
		Resource r = ParseResource(source, true)
		//print(r.str(0))
		
		get_ids(r, lang.ids, r.id)
	
	sort_ids(lang.ids)
	
//	lang.show()


Language load_own_lang()
	Language own
	own.name = conf.own_lang_name
	
	print("loading own language: " + own.name)
	find_all_ids(own)
	
	parse_source_code_dir(conf.source_code_dir, own)
	sort_texts(own.texts)
	print("    " + own.ids.num + " ids, " + own.texts.num + " texts")
	
	return own

Language load_prelang(string filename)
	Language l
	try
		File* f = FileOpenText(filename)
		string t
		int n
		f >> t
		f >> l.name
		f >> t
		f >> n
		f >> t
		for i in 0:n
			LangID id
			f >> id.id
			f >> id.title
			f >> id.tooltip
			l.ids.add(id)
		f >> t
		f >> n
		f >> t
		for i in 0:n
			LangText tt
			f >> tt.orig
			f >> tt.trans
			l.texts.add(tt)
		delete f
		//l.show()
		return l
	except Exception as e
		print("ERROR: " + e.message())

void check_lang(Language l, Language own)
	print("checking " + l.name)
	int nerrors = 0
	
	// check ids
	for i in own.ids
		
		bool found_title = false
		bool found_tooltip = false
		for j in l.ids
			if j.id == i.id
				found_title = (j.title.num > 0)
				found_tooltip = (j.tooltip.num > 0)
				break
		if i.title.num > 0 and !found_title
			//print("    MISSING title for " + i.id)
			nerrors ++
		if i.tooltip.num > 0 and !found_tooltip
			//print("    MISSING tooltip for " + i.id)
			nerrors ++
	
	// check texts
	for t in own.texts
		bool found = false
		for tt in l.texts
			if t.orig == tt.orig
				found = (tt.trans.num > 0)
				break
		if !found
			//print("    MISSING text: " + t.orig)
			nerrors ++
	
	if nerrors == 0
		print("    ok, good translation!")
	else
		print("    " + nerrors + " missing translations")

Language[] load_prelangs()
	Language[] langs
	DirEntry[] files = DirSearch(conf.dir, "*.prelang", false)
	for f in files
		Language l = load_prelang(conf.dir + f.name)
		langs.add(l)
	return langs

void parse_source_code(string source, Language l)
	int pos0 = 0
	while true
		int pos = source.find("_(\"", pos0)
		if pos < 0
			break
		pos += 3
		int length = 0
		for i in 0:1024
			if source[pos + i] == '\'
				i ++
				continue
			if source[pos + i] == '"'     // "
				length = i
				break
		
		LangText t
		t.orig = str_m2utf8(source.substr(pos, length))
		
		bool found = false
		for tt in l.texts
			if tt.orig == t.orig
				found = true
				break
		if !found
			l.texts.add(t)
		//print("    " + source.substr(pos, length))
		pos0 = pos + length

void parse_source_code_dir(string dir, Language l)
	DirEntry[] files = DirSearch(dir, "*.cpp", true)
	for f in files
		if f.is_dir
			parse_source_code_dir(dir + f.name + "/", l)
		else
			//print(dir + f.name)
			string source = FileRead(dir + f.name)
			parse_source_code(source, l)
		
		

void export(string dir)
	conf.set_dir(dir)
	conf.load()
	
	Language own = load_own_lang()
	
	Language[] langs = load_prelangs()
	
	for l in langs
		check_lang(l, own)

void main(string[] arg)

	if arg.num > 0
		if FileIsDirectory(arg[0])
			export(arg[0])
		else if arg[0].tail(4) == ".hui"
			show(arg[0])
		else
			print("???")
