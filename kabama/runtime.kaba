use .common.*
use .parser.*
use .simplify.*
use .expand.*
use .diff.*
use .subst.*
use .zeros.*
use .float.*


class Runtime
	#class Assumption
	#	shared Term term
	struct Function
		var name: string
		var params: string[]
		var formula: shared![Term]
		func __str__() -> string
			var p = params.join(", ")
			return "Function {{name}}({{p}}) := {{formula}}"
	var assumptions: shared![Term][]
	var functions: owned![Function][]
	var history: shared![Term][]
	
	#class Error extends Exception
	
	func mut add_function(t: shared![Term])
		var f = new Function()
		f.name = t.params[0].name
		for p in t.params[0].params
			f.params.add(p.name)
		f.formula = t.params[1]
		functions.add(give(f))
		#print("added... " + str(f))

	func mut eval_term(_t: shared![Term]) -> shared![Term]
		# recursive
		var t = _t.shallow_copy()
		t.params = [for p in t.params eval_term(p)]
			
		# self
		if t.name == "$"
			if len(history) == 0
				raise(new RuntimeError("no previous command yet"))
			return history[len(history)-1]
		else if t.name == "expand"
			return expand(t.params[0])
		else if t.name == "diff"
			try
				return diff(t.params[0], t.params[1].name)
			except
				return give(t)
		else if t.name == "subst"
			return subst(t.params[0], t.params[1], t.params[2], 50)
		else if t.name == "zeros"
			return zeros(t.params[0], t.params[1].name)
		else if t.name == "float"
			return make_float(t.params[0])
		else if t.type == Type.SPECIAL
			if t.name == "def"
				add_function(t.params[0])
		else if t.type == Type.FUNCTION
			for f in functions
				if f.name == t.name
					return subst_variable(f.formula, f.params[0], t.params[0])
				
		return give(t)

	func mut eval(s: string) -> shared![Term]
		let t = parse(s)
		let r = normalize(eval_term(t))
		history.add(r)
		return r
