use common.*
use parser.*
use algebra.BigInt

func add(a: string, b: string) -> string
	#return i2s(s2i(a) + s2i(b))
	let va = BigInt(a)
	let vb = BigInt(b)
	return str(va + vb)

func sub(a: string, b: string) -> string
	#return i2s(s2i(a) - s2i(b))
	let va = BigInt(a)
	let vb = BigInt(b)
	return str(va - vb)

func mul(a: string, b: string) -> string
	#return i2s(s2i(a) * s2i(b))
	let va = BigInt(a)
	let vb = BigInt(b)
	return str(va * vb)

#string mod(string a, string b)
#	#return i2s(s2i(a) * s2i(b))
#	vli va = a, vb = b, rem
#	va.div(b, rem)
#	return rem.str()

func exponent(a: string, b: string) -> string
	let va = BigInt(a)
	let vb = BigInt(b)
	return str(BigInt.pow(va, vb))

func gcd(a: string, b: string) -> string
	let va = BigInt(a)
	let vb = BigInt(b)
	return str(BigInt.gcd(va, vb))

func number_is_even(n: string) -> bool
	return (i32(n[-1:]) % 2) == 0

func append_terms(out a: shared![Term][], b: shared![Term][])
	for x in b
		a.add(x)

func append_neg(out a: bool[], b: bool[], inv: bool)
	for x in b
		if inv
			a.add(!x)
		else
			a.add(x)

func print_group(gr: shared![Term][])
	print("[" + [for x in gr str(x)].join(", ") + "]")


func extract_add_group(t: shared![Term], out negative: bool[]) -> shared![Term][]
	var l: shared![Term][]

	# shortcut (-1)*a  ->  -a
	if t.name == "*"
		if t.params[0].name == "-1"
			var n1: bool[]
			append_terms(l, extract_add_group(t.params[1], n1))
			append_neg(negative, n1, true)
			return l

	if t.name == "+"
		var n0, n1: bool[]
		append_terms(l, extract_add_group(t.params[0], n0))
		append_terms(l, extract_add_group(t.params[1], n1))
		append_neg(negative, n0, false)
		append_neg(negative, n1, false)
	else if t.name == "-"
		var n0, n1: bool[]
		append_terms(l, extract_add_group(t.params[0], n0))
		append_terms(l, extract_add_group(t.params[1], n1))
		append_neg(negative, n0, false)
		append_neg(negative, n1, true)
	else
		negative.add(false)
		l.add(t)
	return l

func extract_product_group(t: shared![Term], out inverse: bool[]) -> shared![Term][]
	var l: shared![Term][]
	if t.name == "*"
		var n0, n1: bool[]
		append_terms(l, extract_product_group(t.params[0], n0))
		append_terms(l, extract_product_group(t.params[1], n1))
		append_neg(inverse, n0, false)
		append_neg(inverse, n1, false)
	else if t.name == "/"
		var n0, n1: bool[]
		append_terms(l, extract_product_group(t.params[0], n0))
		append_terms(l, extract_product_group(t.params[1], n1))
		append_neg(inverse, n0, false)
		append_neg(inverse, n1, true)
	else
		inverse.add(false)
		l.add(t)
	return l

# [2x, 3y, z] -> [2,3,1], [x,y,z]
func split_off_factor(out l: shared![Term][]) -> string[]
	var factor: string[]
	for i=>x in l
		var f = "1"
		if x.name == "*"
			if x.params[0].type == Type.NUMBER
				f = x.params[0].name
				l[i] = x.params[1]
		factor.add(f)
	return factor

# a/b kürzen
func eval_number_division(a: string, b: string) -> xfer[Term]
	var va = BigInt(a)
	var vb = BigInt(b)
	var rem: BigInt
	var tt = va.div(vb, rem)
	let bi0 = BigInt(0)
	if rem == bi0
		return AddNumber(str(tt))
	var g = gcd(a, b)
	if g != "1"
		var vg = BigInt(g)
		va.idiv(vg, tt)
		vb.idiv(vg, tt)
		if vb.sign
			vb.sign = false
			va.sign = !va.sign
		return AddOperator("/", AddNumber(str(va)), AddNumber(str(vb)))
	return AddOperator("/", AddNumber(str(va)), AddNumber(str(vb)))


func literally_equal(a: shared![Term], b: shared![Term]) -> bool
	if a.name != b.name
		return false
	for i in 0:len(a.params)
		if !literally_equal(a.params[i], b.params[i])
			return false
	return true
	
			
	
func sort_group(out l: shared![Term][])
	# "render" all terms...   need a comp(term, term) function
	var s = [for t in l str(t)]
	
	# sorti
	for i in 0:len(l)-1
		for j in i+1:len(l)
			if s[i].compare(s[j]) > 0
				l.swap(i, j)
				s.swap(i, j)



func make_unique_in_product(out l: shared![Term][], inv: bool[])
	var exponents: shared![Term][]
	
	for i=>x in l
		var e: shared![Term] = x # dummy
		if x.name == "^"
			e = x.params[1]
			l[i] = x.params[0]
		else
			e = AddNumber("1")
		if inv[i]
			e = AddOperator("*", AddNumber("-1"), e)
		exponents.add(e)
	for i in 0:len(l)
		for j in i+1:len(l)
			if literally_equal(l[i], l[j])
				exponents[i] = AddOperator("+", exponents[i], exponents[j])
				exponents.remove(j)
				l.remove(j)
				j --
	for i=>x in l
		if exponents[i].name != "1"
			l[i] = AddOperator("^", x, normalize(exponents[i]))


func eval_product(t: shared![Term]) -> shared![Term]
	var inverse: bool[]
	var l = extract_product_group(t, inverse)
	#print_group(l)
	
	
	var num = "1"
	var denum = "1"
	for i in 0:len(l)
		if l[i].type == Type.NUMBER
			if inverse[i]
				denum = mul(denum, l[i].name)
			else
				num = mul(num, l[i].name)
			l.remove(i)
			inverse.remove(i)
			i --
	
	if num == "0"
		return AddNumber("0")
	if len(l) == 0
		return eval_number_division(num, denum)
	
	#print_group(l)
	make_unique_in_product(l, inverse)
	#print_group(l)
	sort_group(l)
	
	var r = l[0]
	for i in 1:len(l)
		r = AddOperator("*", r, l[i])
	if num != "1" or denum != "1"
		r = AddOperator("*", eval_number_division(num, denum), r)
	return r



func make_unique_in_sum(out l: shared![Term][], out negative: bool[])
	var multiplicity: string[]
	#print_group(l)
	
	for i=>x in l
		var m = "1"
		if x.name == "*"
			if x.params[0].type == Type.NUMBER
				m = x.params[0].name
				l[i] = x.params[1]
		if negative[i]
			m = mul(m, "-1")
		multiplicity.add(m)
	for i in 0:len(l)
		for j in i+1:len(l)
			if literally_equal(l[i], l[j])
				multiplicity[i] = add(multiplicity[i], multiplicity[j])
				multiplicity.remove(j)
				l.remove(j)
				j --
	for i=>x in l
		if multiplicity[i] == "0"
			l.remove(i)
			negative.remove(i)
			multiplicity.remove(i)
			i --
	for i=>x in l
		if multiplicity[i] != "1"
			l[i] = AddOperator("*", AddNumber(multiplicity[i]), x)


func eval_sum(t: shared![Term]) -> shared![Term]
	var negative: bool[]
	var l = extract_add_group(t, negative)
	#print_group(l)
	
	# add constants
	var num = "0"
	for i in 0:len(l)
		if l[i].type == Type.NUMBER
			if negative[i]
				num = sub(num, l[i].name)
			else
				num = add(num, l[i].name)
			l.remove(i)
			negative.remove(i)
			i --
	
	

	#print_group(l)
	make_unique_in_sum(l, negative)
	#print_group(l)
	
	# all constant numbers
	if len(l) == 0
		return AddNumber(num)
	
	sort_group(l)
	
	var t_r = l[0]
	for i in 1:len(l)
		t_r = AddOperator("+", t_r, l[i])
	if num != "0"
		if num[:1] == "-"
			return AddOperator("-", t_r, AddNumber(num[1:]))
		else
			return AddOperator("+", t_r, AddNumber(num))
	return t_r

func eval_exponent(t: shared![Term]) -> shared![Term]
	var p0 = t.params[0]
	var p1 = t.params[1]
	if p1.type == Type.NUMBER
		if p1.name == "1"
			return normalize(p0)
		if p1.name == "0"
			return AddNumber("1")
		if p0.type == Type.NUMBER
			return AddNumber(exponent(p0.name, p1.name))
	if p0.name == "*"
		var a = eval_exponent(AddOperator("^", p0.params[0], p1))
		var b = eval_exponent(AddOperator("^", p0.params[1], p1))
		return AddOperator("*", a, b)
	else if p0.name == "/"
		var a = eval_exponent(AddOperator("^", p0.params[0], p1))
		var b = eval_exponent(AddOperator("^", p0.params[1], p1))
		return AddOperator("/", a, b)
	else if p0.name == "^"
		return AddOperator("^", p0.params[0], eval_product(AddOperator("*", p0.params[1], p1)))
	return t



func eval_function(t: shared![Term]) -> shared![Term]
	if t.name == "ln"
		var p = t.params[0]
		if p.name == "^"
			var tt = AddOperator("*", p.params[1], AddFunction("ln", [p.params[0]]))
			return normalize(give(tt))
		if p.name == "e"
			return AddNumber("1")
		if p.name == "exp"
			return p.params[0]
	if t.name == "sin"
		var p = t.params[0]
		if p.name in ["0", "pi"]
			return AddNumber("0")
		if p.name == "*"
			if p.params[0].type == Type.NUMBER and p.params[1].name == "pi"
				return AddNumber("0")
	if t.name == "cos"
		var p = t.params[0]
		if p.name == "0"
			return AddNumber("1")
		if p.name == "pi"
			return AddNumber("-1")
		if p.name == "*"
			if p.params[0].type == Type.NUMBER and p.params[1].name == "pi"
				if number_is_even(p.params[0].name)
					return AddNumber("1")
				else
					return AddNumber("-1")
	if t.name == "exp"
		var p = t.params[0]
		if p.name == "0"
			return AddNumber("1")
		if p.name == "1"
			return AddConstant("e")
	if t.name == "sqrt"
		var p = t.params[0]
		if p.name == "^"
			if p.params[1].name == "2"
				return normalize(p.params[0])
		if p.name in ["1", "0"]
			return normalize(p)
		#if p.type == Type.NUMBER
			# test... apply
			#return AddNumber("...")
	if t.name == "atan"
		var p = t.params[0]
		if p.name == "0"
			return AddNumber("0")
		if p.name == "1"
			return AddOperator("/", AddConstant("pi"), AddNumber("4"))
		if p.name == "-1"
			return normalize(AddOperator("-", AddNumber("0"), AddOperator("/", AddConstant("pi"), AddNumber("4"))))
	if t.name == "deg"
		var p = t.params[0]
		if p.type == Type.NUMBER
			return AddNumber("0")
		if p.name == "∧"
			return normalize(AddOperator("+", AddFunction("deg", [p.params[0]]),
			                                  AddFunction("deg", [p.params[1]])))
		
	return t

func eval_wedge(_t: shared![Term]) -> shared![Term]
	var t: shared![Term] = _t.shallow_copy()

	# recursion
	for i in 0:len(t.params)
		t.params[i] = normalize(t.params[i])

	if str(t.params[0]).compare(str(t.params[1])) > 0
		t.params.swap(0, 1)
		                                         #AddOperator("*", AddFunction("deg", [t.params[0]]),
		                                         #                 AddFunction("deg", [t.params[1]])))
		return normalize(AddOperator("*",
		                             AddOperator("^",
		                                         AddNumber("-1"),
		                                         AddOperator("*",
		                                                     AddFunction("deg", [t.params[0]]),
		                                                     AddFunction("deg", [t.params[1]]))),
		                             t))
		
	return t

func eval_equal(t: shared![Term]) -> shared![Term]
	if literally_equal(normalize(t.params[0]), normalize(t.params[1]))
		return AddVariable("true")
	else if t.params[0].type == Type.NUMBER and t.params[1].type == Type.NUMBER
		return AddVariable("false")
	return t

func eval_in(t: shared![Term]) -> shared![Term]
	if t.params[0].type == Type.NUMBER and t.params[1].name in ["N", "Z"]
		return AddVariable("true")
	return t

func normalize(_t: shared![Term]) -> shared![Term]
	var t: shared![Term] = _t.shallow_copy()

	# recursion
	for i in 0:len(t.params)
		t.params[i] = normalize(t.params[i])
		
	# self
	if t.type == Type.FUNCTION
		return eval_function(t)
	if t.name in ["*", "/"]
		return eval_product(t)
	if t.name in ["+", "-"]
		return eval_sum(t)
	if t.name == "∧"
		return eval_wedge(t)
	if t.name == "^"
		return eval_exponent(t)
	if t.name == "="
		return eval_equal(t)
	if t.name == ":"
		return eval_in(t)
	return t


func main()
	var e = parse("1+1")
	print("n")
	var t = normalize(e)
	print(str(t))
	