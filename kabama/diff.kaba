use common
use parser
use simplify


func diff_function(f: string, params: Term shared[]) -> Term shared
	if f == "exp"
		return AddFunction("exp", params)
	if f == "sin"
		return AddFunction("cos", params)
	if f == "cos"
		return AddOperator("-", AddNumber("0"), AddFunction("sin", params))
	if f == "sinh"
		return AddFunction("cosh", params)
	if f == "cosh"
		return AddFunction("sinh", params)
	if f == "ln"
		return AddOperator("/", AddNumber("1"), params[0])
	if f == "atan"
		return AddOperator("/", AddNumber("1"), AddOperator("+", AddNumber("1"), AddOperator("^", params[0], AddNumber("2"))))
	if f == "sqrt"
		return AddOperator("/", AddNumber("1"), AddFunction("sqrt", params))
	raise(new RuntimeError("can not differentiate function '{{f}}'"))


# d/dx
# always copies...
func diff(t: Term shared, by: string) -> Term shared

	# self
	if t.type == Type.NUMBER or t.type == Type.CONSTANT or t.type == Type.FLOAT # in [Type.NUMBER, Type.FLOAT, Type.CONSTANT]
		return AddNumber("0")
	if t.type == Type.VARIABLE
		if t.name == by
			return AddNumber("1")
		return AddNumber("0")
	if t.type == Type.FUNCTION
		var r = diff_function(t.name, t.params)
		for p in t.params
			r = AddOperator("*", diff(p, by), r)
		# diff(f(a))  ->  f'(a) * diff(a)
		return r
	if t.type == Type.OPERATOR
		var a, b: Term shared
		if len(t.params) > 0
			a = t.params[0]
		if len(t.params) > 1
			b = t.params[1]
		if t.name in ["+", "-", "="]
			# diff(a + b)  ->  diff(a) + diff(b)
			return AddOperator(t.name, diff(a, by), diff(b, by))
		if t.name == "*"
			# diff(a * b)  ->  diff(a) * b + a * diff(b)
			return AddOperator("+", AddOperator("*", diff(a, by), b),
			                        AddOperator("*", a, diff(b, by)))
		if t.name == "/"
			# diff(a / b)  ->  diff(a) / b - a * diff(b) / b^2
			return AddOperator("-", AddOperator("/", diff(a, by), b),
			                        AddOperator("/", AddOperator("*", a, diff(b, by)),
			                                         AddOperator("^", b, AddNumber("2"))))
		if t.name == "^"
			if b.type == Type.NUMBER or b.type == Type.FLOAT or b.type == Type.CONSTANT # in [...]
				# diff(a ^ b)  ->  b * ((a ^ (b - 1)) * diff(a))
				return AddOperator("*", b,
				                        AddOperator("*", AddOperator("^", a, AddNumber(sub(b.name, "1"))),
				                                         diff(a, by)))
			# diff(a ^ b)  ->  b * ((a ^ (b - 1)) * diff(a)) + ln(a) * a^b * diff(b)
			return AddOperator("+", AddOperator("*", b,
			                                         AddOperator("*", AddOperator("^", a, AddOperator("-", b, AddNumber("1"))),
			                                                          diff(a, by))),
			                        AddOperator("*", AddFunction("ln", [a]),
			                                         AddOperator("*", AddOperator("^", a, b),
			                                                          diff(b, by))))
	if t.type == Type.LIST
		var r = t.shallow_copy()
		for i=>p in r.params
			r.params[i] = diff(p, by)
		return r
	raise(new RuntimeError("unhandled differential..."))
