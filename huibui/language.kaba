use config.*
use os
use os.Path


struct LangID
	var id: string
	var title: string
	var tooltip: string

struct LangText
	var orig: string
	var trans: string

struct Language
	var filename: Path
	var name: string
	var ids: LangID[]
	var texts: LangText[]
	func show()
		print(name)
		for i in ids
			print(i.id)
			print("\t{{i.title}}")
			print("\t{{i.tooltip}}")
			
		for t in texts
			print(t.orig)
			print("\t{{t.trans}}")
	func mut add_id(id: string, title: string, tooltip: string)
		for i in ids
			if i.id == id
				if len(title) > 0
					i.title = title
				if len(tooltip) > 0
					i.tooltip = tooltip
				return
		var i: LangID
		i.id = id
		i.title = title
		i.tooltip = tooltip
		ids.add(i)
	func mut add_text(orig: string, trans: string)
		for t in texts
			if t.orig == orig
				if len(trans) > 0
					t.trans = trans
				return
		var t: LangText
		t.orig = orig
		t.trans = trans
		texts.add(t)
		
	func save()
		var f = os.fs.open(filename, "wt")
		f << "# Language"
		f << name
		f << "# Num IDs"
		f << len(ids)
		f << "# Data (ID,Text,Tooltip)"
		for i in ids
			f << i.id
			f << i.title.escape()
			f << i.tooltip.escape()
		f << "# Num Language Strings"
		f << len(texts)
		f << "# Data (ID,Text)"
		for t in texts
			f << t.orig.escape()
			f << t.trans.escape()


	func mut sort()
		sort_ids(ids)
		sort_texts(texts)
	func get_useful_ids() -> LangID[]
		var uids: LangID[]
		for i in ids
			if len(i.title) > 0 or len(i.tooltip) > 0
				uids.add(i)
		sort_ids(uids)
		return uids
	func get_useful_texts() -> LangText[]
		var utexts: LangText[]
		for t in texts
			if len(t.trans) > 0
				utexts.add(t)
		sort_texts(utexts)
		return utexts



func load_prelang(filename: Path) -> Language
	var l: Language
	l.filename = filename
	#try
	if true
		var f = os.fs.open(filename, "rt")
		var t: string
		var n: int
		f >> t
		f >> l.name
		f >> t
		f >> n
		f >> t
		for i in 0:n
			var id: LangID
			f >> id.id
			f >> t
			id.title = t.unescape()
			f >> t
			id.tooltip = t.unescape()
			l.ids.add(id)
		f >> t
		f >> n
		f >> t
		for i in 0:n
			var tt: LangText
			f >> t
			tt.orig = t.unescape()
			f >> t
			tt.trans = t.unescape()
			l.texts.add(tt)
		#l.show()
		return l
	#except Exception as e
	#	print("ERROR: " + e.message())


func load_prelangs() -> Language[]
	var langs: Language[]
	var files = os.fs.search(conf.dir, "*.prelang", "f")
	for f in files
		var l = load_prelang(conf.dir | f)
		langs.add(l)
	return langs

func lang_id_add(out ids: LangID[], id: string, title: string, tooltip: string, ns: string)
	if id == "?" or id == ""
		return
	var cid = id
	if not cid.find("/", 0).has_value()
		#if ns != id
		cid = "{{ns}}/{{id}}"
	for i in ids
		if i.id == cid
			return
	var a: LangID
	a.id = cid
	a.title = title
	a.tooltip = tooltip
	ids.add(a)

func sort_ids(out ids: LangID[])
	for i in 0:len(ids)
		for j in i+1:len(ids)
			if ids[i].id > ids[j].id
				var t = ids[i]
				ids[i] = ids[j]
				ids[j] = t


func sort_texts(out texts: LangText[])
	for i in 0:len(texts)
		for j in i+1:len(texts)
			if texts[i].orig > texts[j].orig
				var t = texts[i]
				texts[i] = texts[j]
				texts[j] = t



func check_lang(l: Language, own: Language, create_file: bool) -> bool
	print("checking {{l.name}}")
	var nerrors = 0
	
	var file: owned[os.FileStream]
	if create_file
		file = os.fs.open(conf.dir | "TRANS", "wt")
	
	# check ids
	for i in own.ids
		
		var found_title = false
		var found_tooltip = false
		for j in l.ids
			if j.id == i.id
				found_title = (len(j.title) > 0)
				found_tooltip = (len(j.tooltip) > 0)
				break
		if len(i.title) > 0 and !found_title
			for f in file
				f << "# id:{{i.id}}"
				f << i.title.escape()
				f << ""
			#print("    MISSING title for " + i.id)
			nerrors ++
		if len(i.tooltip) > 0 and !found_tooltip
			for f in file
				f << "# tooltip:{{i.id}}"
				f << i.tooltip.escape()
				f << ""
			#print("    MISSING tooltip for {{i.id}}")
			nerrors ++
	
	# check texts
	for t in own.texts
		var found = false
		for tt in l.texts
			if t.orig == tt.orig
				found = (len(tt.trans) > 0)
				break
		if !found
			for f in file
				f << "# text"
				f << t.orig.escape()
				f << ""
			#print("    MISSING text: " + t.orig)
			nerrors ++
	
	if nerrors == 0
		print("    ok, good translation!")
	else
		print("    {{nerrors}} missing translations")

	return nerrors == 0


