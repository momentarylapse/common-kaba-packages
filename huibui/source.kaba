use os
use config
use language

class SourceParser
	var source: string
	func parse()
		int pos0 = 0
		while true
			int pos = source.find("_(\"", pos0)
			if pos < 0
				break
			pos += 3
			int length = 0
			for i in 0:1024
				if source[pos + i] == '\\'
					i ++
					continue
				if source[pos + i] == '"'     # "
					length = i
					break
			handle(pos, length)
			#print("    " + source.substr(pos, length))
			pos0 = pos + length


	func parse_source_code_dir(dir: Path)
		var files = Filesystem.search(dir, "*.cpp", "fd")
		for f in files
			if Filesystem.is_directory(dir << f)
				parse_source_code_dir(dir << f)
			else
				source = Filesystem.read(dir << f)
				begin()
				parse()
				end(dir << f)

	func virtual handle(pos: int, length: int)
	func virtual begin()
	func virtual end(filename: Path)

class SourceLanguageFinder extends SourceParser
	var l: Language*
	func override handle(pos: int, length: int)
		LangText t
		t.orig = str_m2utf8(source[pos:pos+length]).unescape()
		
		if len(t.orig) > 0
			bool found = false
			for tt in l.texts
				if tt.orig == t.orig
					found = true
					break
			if !found
				l.texts.add(t)

class SourceSwapper extends SourceParser
	var l: Language*
	var source_new: string
	var changed: bool
	var delta: int
	func override begin()
		source_new = source
		changed = false
		delta = 0
	func override handle(pos: int, length: int)
		string orig = str_m2utf8(source[pos:pos+length]).unescape()
		
		for t in l.texts
			if orig == t.orig
				string r = t.trans.escape()
				print("REPLACE {{orig}}    ->   {{r}}")
				
				int ppos = pos + delta
				source_new = source_new.head(ppos) + r + source_new[ppos+length:]
				
				delta += len(r) - length
				
				changed = true
	func override end(filename: Path)
		if !changed
			return
		#print(">>>>>>>>>>>")
		var f = Filesystem.create(filename)
		f.write(source_new)
		del f
				


func parse_source_code_dir(dir: Path, l: Language)
	SourceLanguageFinder p
	p.l = &l
	p.parse_source_code_dir(dir)


func swap_translate_source(dir: Path, l: Language)
	SourceSwapper p
	p.l = &l
	p.parse_source_code_dir(dir)

